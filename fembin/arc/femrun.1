#!/bin/csh 
#
# testrun : runs FEM model
#
# basic directories setting

#---------------------------------------------------------------
set defdir         = "/usr/users/georg"
#---------------------------------------------------------------
set tmpdir         = "$defdir/tmp/"
set testbat        = "$defdir/fem/bin/fembat"
set overall_log    = "$defdir/adm/overall"
#---------------------------------------------------------------

####################################################### are there 2 arguments ?

if( $#argv < 2 ) then
	echo "Usage : femrun   HP-routine   STR-file   [time]"
	exit 1
endif

if( $#argv > 2 ) then
	set givenattime = YES
else
	set givenattime = NO
endif

################################################################# set variables

set program = $2
set hpfile =  $1
set strfile = $2.str

shift
shift
if( $givenattime == YES ) then
	set attime = "$*"
else
	set attime = ""
endif

##################################################### control if files do exist

if( ! -e $hpfile ) then
	echo No such file : $hpfile
	exit 1
else if( ! -e $strfile ) then
	echo No such file : $strfile
	exit 1
endif

############################## find name of simulation and set name of log-file

set simulation=`simnam $strfile`

if( $simulation == "" ) then
	set logfile = $program.log
else
	set logfile = $simulation.log
endif

######################################################## echo names to terminal

echo " "
echo "FEM routine          : " $hpfile
echo "input parameter file : " $strfile
echo "name of simulation   : " $simulation
echo "name of log file     : " $logfile
echo "time                 : " $attime
echo " "

##################################################### write to overall log file

echo `date "+%d.%m.%y %H:%M:%S"` $simulation `basename $strfile .str` \
		$hpfile `hostname` $attime >> $overall_log

############################################################### remove log file

if( -e $logfile ) then
  rm $logfile
endif

##################################################################### run model

if ( $givenattime == NO ) then
  nohup $testbat $hpfile $strfile >& $logfile &
else
  set aux = `date +%m%d.%H%M`
  set tmpfil = $tmpdir/hpat.$aux.$$
  set actdir = `pwd`
  echo "cd $actdir" > $tmpfil
  echo "nohup $testbat $hpfile $strfile > $logfile 2>&1 &" >> $tmpfil
  at $attime $tmpfil >& /dev/null
  if ( $status ) then	#error in previous at command -> repeat for xfem
    at $attime $tmpfil
  endif
endif

########################################################### now we are finished

exit 0
