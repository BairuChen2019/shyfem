C****************************************************************************
C
C                  SED96:  A SEDIMENT TRANSPORT MODEL
C                           FOR CONTINENTAL SHELVES
C
C                  GEOLOGICAL SURVEY OF CANADA (ATLANTIC)
C                     BEDFORD INSTITUTE OF OCEANOGRAPHY
C
C****************************************************************************
C
C Revision Log:
C
C Jan, 2004     ccf     (sedtrans-c1.f)
C                       This file is based on sedtrans-b6.f
C                       Change critical share velocity for suspension
C                       and fall velocity (THRESH). Delete SMITH method
C                       and Yalin for ACKERS-WHITE. Changes in FRICFAC. 
C			Adapt to SEDTRANS96
c 05.03.2004    ggu     integrated changes into main model
C
C****************************************************************************
C****************************************************************************
C                  *** SUBROUTINE SEDISDGM ***
C****************************************************************************
C****************************************************************************

      SUBROUTINE SEDISDGM(D,UZ,Z,CDIR,HT,PER,WDIR,GD,RHINP,RLINP,
     @BETA,RHOS,RHOW,IOPT1,FRACT,CONC0,TAOCE,TAOCD,TIMEDR,
     @WS,PRS,RKERO,ZB0,AULVA,WSULVA,                   !**INPUT VARIABLES**

     @UB,AB,WL,FCW,DELTACW,UA,U100,PHIB,USTCS,USTWS,USTCWS,USTCWSB,
     @USTC,USTW,USTCW,Z0,Z0C,RHEIGHT,RLENGTH,USTCRB,USTCRS,TS1,TB1,
     @TS2,TB2,PERBED,PERSUSP,QS,QSDIR,C0,C0A,SEDM,SED,SEDDIR,CONC,RD0,
     @RD,USTUP,FALL,USTCWSE,RE0,RE,TIME0,QS0,PHIM,TAOCWS,TAOS,RES)  !**OUTPUT VARIABLES**

      IMPLICIT NONE

C************* INPUT VARIABLES ******************

      INTEGER IOPT1             !SEDIMENT TRANSPORT FORMULA OPTION NUMBER
      DOUBLE PRECISION D        !WATER DEPTH (M)
      DOUBLE PRECISION UZ       !AMBIENT CURRENT AT HEIGHT Z ABOVE THE SEAFLOOR (M/S)
      DOUBLE PRECISION Z        !HEIGHT OF UZ ABOVE SEAFLOOR
      DOUBLE PRECISION CDIR     !DIRECTION OF AMBIENT CURRENT (DEGREES TRUE)
      DOUBLE PRECISION HT       !WAVE HEIGHT (M)
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION WDIR     !WAVE PROPAGATION DIRECTION (DEGREES TRUE)
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION RHINP    !INPUT RIPPLE HEIGHT (M)
      DOUBLE PRECISION RLINP    !INPUT RIPPLE LENGTH (M)
      DOUBLE PRECISION BETA     !BED SLOPE (DEGREE)
      DOUBLE PRECISION RHOS     !DENSITY OF SEDIMENT MINERAL(S)   (KG/M**3)
      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION FRACT    !FRACTION OF THE TOTAL SEDIMENT WITH GRAIN SIZE GD
      DOUBLE PRECISION CONC0    !INITIAL ESTIMATE OF SEDIMENT CONCENTRATION (ie mg/l)
      DOUBLE PRECISION TAOCE    !CRITICAL STRESS FOR EROSION (Pa)
      DOUBLE PRECISION TAOCD    !CRITICAL STRESS FOR DEPOSITION (Pa)
      DOUBLE PRECISION TIMEDR   !DEPOSITION OR EROSION DURATION (minutes)
      DOUBLE PRECISION WS       !SETTLING VELOCITY FOR COHESIVE SEDIMENT (m/s)
      DOUBLE PRECISION PRS      !PROBABILITY OF RESUSPENSION (NORMALLY ASSUMED = 0)
      DOUBLE PRECISION RKERO    !PROPORTIONALITY COEFF FOR EROSION RATE (DEFAULT = 1.62)
      DOUBLE PRECISION ZB0      !INITIAL DEPTH OF THE LAYER EXPOSED TO THE FLOW
      DOUBLE PRECISION AULVA    !PERCENTAGE OF AREA COVERED BY THE ALGAE 'ULVA' (%)
      DOUBLE PRECISION WSULVA   !SETTLING VELOCITY OF ULVA (M/S)
      DOUBLE PRECISION VISC     !DYNAMIC VISCOSITY OF THE FLUID (KG/M*SEC (OR N.S/M**2))

C************* OUTPUT VARIABLES *****************

      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)
      DOUBLE PRECISION AB       !EXCURSION LENGTH OF BOTTOM WAVE ORBIT (M)
      DOUBLE PRECISION WL       !WAVE LENGTH (M)
      DOUBLE PRECISION FCW      !BOTTOM (SKIN) FRICTION FACTOR
      DOUBLE PRECISION DELTACW  !HEIGHT OF THE WAVE-CURRENT BOUNDARY LAYER
      DOUBLE PRECISION UA       !CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
      DOUBLE PRECISION PHIB     !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS (RADIANS)
      DOUBLE PRECISION U100     !CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTWS	!WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWSE	!EFFECTIVE COMBINED SKIN-FRICTION SHEAR VELOCITY
      DOUBLE PRECISION USTC	!TOTAL CURRENT SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTW	!TOTAL WAVE SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCW	!COMBINED TOTAL SHEAR VELOCITY OF GM
      DOUBLE PRECISION Z0	!BED ROUGHNESS LENGTH (M)
      DOUBLE PRECISION Z0C	!APPARENT BED ROUGHNESS LENGTH (M)
      DOUBLE PRECISION RHEIGHT	!PREDICTED RIPPLE HEIGHT
      DOUBLE PRECISION RLENGTH	!PREDICTED RIPPLE LENGTH
      DOUBLE PRECISION USTCRB	!CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTCRS	!CRITICAL SHEAR VEL FOR INITI OF SUSPENDED LOAD TRANSPORT (M/SEC)
      DOUBLE PRECISION TB1	!TIME AT WHICH BEDLOAD TRANSPORT CEASES (SEC)
      DOUBLE PRECISION TB2	!TIME AT WHICH BEDLOAD TRANSPORT RECOMMENCES (SEC)
      DOUBLE PRECISION TS1	!TIME AT WHICH SUSPENDED LOAD TRANSPORT CEASES (SEC)
      DOUBLE PRECISION TS2	!TIME AT WHICH SUSPENDED LOAD TRANSPORT RECOMMENCES (SEC)
      DOUBLE PRECISION PERBED	!PERCENTAGE OF TIME SPENT IN ONLY BEDLOAD TRANSPORT PHASE
      DOUBLE PRECISION PERSUSP  !PERCENTAGE OF TIME SPENT IN SUSPENDED LOAD TRANSPORT PHASE
      DOUBLE PRECISION QS	!SUSPENDED SEDIMENT TRANSPORT RATE (KG/M/S)
      DOUBLE PRECISION QSDIR	!DIRECTION OF SUSPENDED SEDIMENT TRANSPORT (DEGREE)
      DOUBLE PRECISION C0       !REFERENCE CONCENTRATION AT Z0 (KG/M^3)
      DOUBLE PRECISION C0A      !DEPTH AVERAGED REFERENCE CONCENTRATION AT Z0 (KG/M^3)
      DOUBLE PRECISION SED	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME (M**3/S/M)
      DOUBLE PRECISION SEDM	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS MASS (KG/S/M)
      DOUBLE PRECISION SEDDIR	!DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)
      DOUBLE PRECISION CONC	!FINAL CALCULATED SEDIMENT CONCENTRATION (mg/l)
      DOUBLE PRECISION RD0	!INITIAL DEPOSITION RATE (kg/m^2/s)
      DOUBLE PRECISION RD	!DEPOSITION RATE (kg/m^2/s)
      DOUBLE PRECISION RE0	!INITIAL EROSION RATE (kg/m^2/s)
      DOUBLE PRECISION RE	!EROSION RATE (kg/m^2/s)
      DOUBLE PRECISION TIME0	!CALCULATED TIME (minutes) WHEN CONC. BE LESS THAN 1 MG/L
      DOUBLE PRECISION QS0	!INITIAL MASS SEDIMENT TRANSPORT RATE (kg/m/s)
      DOUBLE PRECISION PHIM	!INTERNAL FRICTION ANGLE
      DOUBLE PRECISION TAOCWS	!AVERAGED EFFECTIVE SHEAR STRESS
      DOUBLE PRECISION TAOS	!SOLID TRANSMITTED STRESS DUE TO ULVA
      DOUBLE PRECISION RES	!ULVA RESUSPENSION PARAMETER
      DOUBLE PRECISION USTCWSB	!TRANSPORT-RELATED COMBINED SHEAR VELOCITY
      DOUBLE PRECISION FALL     !SETTLING VELOCITY FOR NON-COHESIVE SEDIMENT (M/SEC)
      DOUBLE PRECISION USTUP	!CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      
C************* INTERMEDIATE VARIABLES *****************

      LOGICAL COHESIVE		!YES=COHESIVE SEDIMENT
      DOUBLE PRECISION PHI100	!ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M. ABOVE SEABED
      DOUBLE PRECISION RPLCOEF	!RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION GAMMA0	!SAND RESUSPENSION COEFFICIENT
      DOUBLE PRECISION PHIM0	!INITIAL INTERNAL FRICTION ANGLE
      DOUBLE PRECISION VISK     !KINEMATIC VISCOSITY OF FLUID (M**2/SEC)
      DOUBLE PRECISION DX       !DIMENSIONLESS GRAIN SIZE

C**************************************************************************
C  INITIALIZE UNASSIGNED VARIABLES

      PHIM = 0.
      PHIM0 = 0.
      TAOS = 0.
      TIME0 = 0.
      SED = 0.
      SEDM = 0.
      TAOCWS = 0.
      CONC = 0.
      RES = 0.
      USTCRB=0.
      USTCRS=0.
      USTUP=0.
      FALL=0.
      TB1=0.                  !ccf INTEL
      TB2=0.                  !ccf INTEL
      TS1=0.                  !ccf INTEL
      TS2=0.                  !ccf INTEL
      PERBED=0.               !ccf INTEL
      PERSUSP=0.              !ccf INTEL

      VISC=1.3D-3
      VISK=VISC/RHOW

      COHESIVE = IOPT1 .EQ. 7	!COHESIVE SEDIMENT

C**************************************************************************
C CALCULATE WAVE-INDUCED BOTTOM PARTICLE VELOCITY AND ORBITAL DIAMETER
C
C OUTPUT VARIABLES:
C
C    UB = MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)
C    AB = EXCURSION LENGTH OF BOTTOM WAVE ORBIT (M)
C               (1/2 OF THE ORBITAL DIAMETER)
C    WL = WAVE LENGTH (M)

      CALL OSCIL(HT,PER,D,UB,AB,WL)

C***************************************************************************
C CALCULATE THRESHOLD CRITERIA FOR BEDLOAD, SUSPENDED LOAD AND
C SHEET FLOW NON-COHESIVE SEDIMENT TRANSPORT.
C THRESH SUBROUTINE NOT APPLICABLE FOR COHESIVE SEDIMENTS --> GOTO FRICFAC
C
C OUTPUT VARIABLES:
C
C        FALL = SETTLING VELOCITY FOR NON-COHESIVE SEDIMENT (M/SEC)
C      USTCRB = CRITICAL SHEAR VELOCITY FOR INITIATION OF BEDLOAD
C               TRANSPORT (M/SEC)
C      USTCRS = CRITICAL SHEAR VELOCITY FOR INITIATION OF SUSPENDED
C               LOAD TRANSPORT (M/SEC)
C       USTUP = CRITICAL SHEAR VELOCITY FOR INITIATION OF SHEET FLOW
C               TRANSPORT (M/SEC)
C          DX = DIMENSIONLESS GRAIN SIZE

      IF( .NOT. COHESIVE ) CALL THRESH(VISK,GD,RHOS,RHOW,FALL,USTCRB,
     $USTCRS,USTUP,DX)

C**************************************************************************
C CALCULATE FRICTION FACTOR, AMBIENT CURRENT AND BOTTOM STRESSES
C
C OUTPUT VARIABLES:
C
c          Z0 = BED ROUGHNESS LENGTH (M)
C         Z0C = APPARENT BED ROUGHNESS LENGTH (M)
C         FCW = BOTTOM (SKIN) FRICTION FACTOR
C          UA = CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
C        PHIB = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS WITHIN THE
C               WAVE BOUNDARY LAYER (RADIANS)
C        U100 = CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
C      PHI100 = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M.
C               ABOVE SEABED (RADIANS)
C               NOTE:  PHI100 = PHIZ AS LONG AS PHIZ IS MEASURED
C               OUTSIDE THE WAVE BOUNDARY LAYER.
C       USTCS = CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
C       USTWS = WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
C      USTCWS = COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
C     USTCWSE = EFFECTIVE COMBINED SKIN-FRICTION SHEAR VELOCITY
C     USTCWSB = TRANSPORT-RELATED COMBINED SHEAR VELOCITY
C        USTC = TOTAL CURRENT SHEAR VELOCITY OF GM 
C        USTW = TOTAL WAVE SHEAR VELOCITY OF GM
C       USTCW = COMBINED TOTAL SHEAR VELOCITY OF GM
C     DELTACW = HEIGHT OF THE WAVE-CURRENT BOUNDARY LAYER
C     RHEIGHT = PREDICTED RIPPLE HEIGHT
C     RLENGTH = PREDICTED RIPPLE LENGTH
C     RPLCOEF = RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION 

      CALL FRICFAC(UZ,Z,CDIR,UB,AB,PER,WDIR,GD,RHINP,RLINP,
     @RHOW,RHOS,USTCRB,USTUP,VISK,Z0,Z0C,FCW,UA,PHIB,U100,PHI100,
     @USTCS,USTWS,USTCWS,USTCWSE,USTCWSB,USTC,USTW,USTCW,DELTACW,
     @RHEIGHT,RLENGTH,RPLCOEF)

C**************************************************************************
C CALCULATE THE DURATION OF THE DIFFERENT SEDIMENT TRANSPORT PHASES
C
C OUTPUT VARIABLES:
C
C         TB1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD
C               TRANSPORT CEASES (SEC)
C         TB2 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD
C               TRANSPORT RECOMMENCES (SEC)
C         TS1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH SUSPENDED
C               LOAD TRANSPORT CEASES (SEC)
C         TS2 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH SUSPENDED
C               LOAD TRANSPORT RECOMMENCES (SEC)
C      PERBED = PERCENTAGE OF TIME SPENT IN ONLY BEDLOAD TRANSPORT PHASE
C     PERSUSP = PERCENTAGE OF TIME SPENT IN SUSPENDED LOAD TRANSPORT PHASE
C      
C TIMING.FOR AND PROFL.FOR ARE NOT NEEDED FOR COHESIVE SEDIMENT

      IF ( .NOT. COHESIVE ) THEN

      CALL TIMING(RPLCOEF,RHOW,UA,PHIB,UB,PER,U100,USTCRB,
     @USTCRS,USTCS,USTWS,USTCWS,USTCWSB,TB1,TB2,TS1,TS2,PERBED,
     @PERSUSP)

C CALCULATE VELOCITY PROFILE, SUSPENDED SEDIMENT CONCENTRATION PROFILE
C AND THE SUSPENDED SEDIMENT TRANSPORT RATE AND DIRECTION

      CALL PROFL(D,RHOW,FALL,UB,CDIR,USTCS,USTWS,USTCWS,
     @USTCWSB,USTCWSE,USTC,USTW,USTCW,USTCRB,USTCRS,USTUP,Z0,Z0C,
     @DELTACW,C0,GAMMA0,QS,QSDIR,C0A)

      ENDIF

C**************************************************************************
C CALCULATE SEDIMENT TRANSPORT RATE AND DIRECTION
C
C OUTPUT VARIABLES:
C
C    SED = TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME OF SEDIMENT SOLIDS 
C          TRANSPORTED PER UNIT BED WIDTH PER UNIT TIME (M**3/S/M)
C    SEDM = TIME-AVERAGED NET SEDIMENT TRANSPORT AS MASS OF SEDIMENT SOLIDS 
C           TRANSPORTED PER UNIT BED WIDTH PER UNIT TIME (KG/S/M)
C    SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

      CALL TRANSPO(D,UA,UB,U100,PHIB,PHI100,FCW,PER,GD,FRACT,BETA,
     @RHOS,RHOW,USTCRB,USTCS,USTWS,USTCWS,USTCWSB,RPLCOEF,HT,
     @CDIR,WDIR,TB1,TB2,TS1,PERBED,PERSUSP,IOPT1,VISK,DX,FALL,CONC0,
     @TAOCD,TAOCE,TIMEDR,WS,RD0,RD,RE0,RE,TIME0,QS0,SED,SEDM,SEDDIR,
     @CONC,ZB0,PHIM0,PHIM,AULVA,WSULVA,RKERO,TAOS,RES,TAOCWS,USTUP)

      RETURN
      END

C****************************************************************************
C****************************************************************************

C **********************************************************
C SUBROUTINE OSCIL
C **********************************************************
      SUBROUTINE OSCIL(HT,PER,D,UB,AB,WL)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C  THIS SUBROUTINE CALCULATES WAVE-INDUCED BOTTOM PARTICLE VELOCITY
C  AND DISPLACEMENT USING LINEAR WAVE THEORY.  A CHECK IS ALSO MADE
C  FOR WAVE BREAKING.
C
C  INPUT VARIABLES:
C         HT = WAVE HEIGHT (M)       (INDATA96 file)
C        PER = WAVE PERIOD (SEC)     (INDATA96 file)
C          D = WATER DEPTH (M)       (INDATA96 file)
C
C  OUTPUT VARIABLES:
C         UB = MAX. WAVE-INDUCED BOTTOM HORIZ. PARTICLE VELOCITY (M/SEC)
C         AB = MAX. WAVE-INDUCED BOTTOM HORIZ. PARTICLE DISPLACEMENT (M)
C              (1/2 OF THE ORBIT SIZE)
C         WL = WAVELENGTH FROM LWT DISPERSION EQUATION (M)
C
C  INTERMEDIATE VARIABLES:
C          G = ACCELERATION DUE TO GRAVITY (M/SEC**2)
C        WL0 = DEEP WATER WAVE LENGTH (M)
C          W = WAVE ANGULAR FREQUENCY (RAD/SEC)
C        RKD = K*D  ( K = WAVE NUMBER (RAD/M) )
C         HB = BREAKING WAVE HEIGHT FOR GIVEN WAVE PERIOD AND WATER DEPTH (M)

      G=9.81
      PI=2.*ASIN(1.)
      UB=0.0
      AB=0.0
      WL=0.0

C---------------------------------------------------------------------
C CHECK FOR CURRENT ONLY CASE (INCLUDING 'DEEP WATER' WAVE CONDITIONS)
C
C   FOR DEEP WATER WAVE CONDITIONS THE NORMAL CRITERION IS D/WL GREATER
C   THAN 0.5. TO ENSURE NO APPRECIABLE WAVE INDUCED BOTTOM STRESS, THIS
C   CODE USES THE CRITERION OF D/WL GREATER THAN 2.0

      IF (PER .EQ. 0) GOTO 30
      WL0 = G*PER*PER/(2*PI)

      IF ((D/WL0) .GT. 2.) RETURN

C---------------------------------------------------------------------
C  CALCULATE WAVELENGTH BY NEWTON-RAPHSON SOLUTION OF LWT DISPERSION
C  EQUATION.

      W=2.*PI/PER
      RKD0=W**2*D/G
      RKD=RKD0
  20  CONTINUE
      DKD=(1./TANH(RKD)-RKD/RKD0)/(1./RKD0+1./SINH(RKD)**2)
      RKD=RKD+DKD
      IF (ABS(DKD) .GE. 1.0E-4) GO TO 20
      WL=WL0*TANH(RKD)

C---------------------------------------------------------------------
C  CALCULATE WAVE-INDUCED BOTTOM PARTICLE VELOCITY AND ORBIT SIZE

C CONVENTIONALLY: UB=HT*G*PER/(2*WL*COSH(2*PI*D/WL))
C THE MORE EFFICIENT ALGORITH IS:
      UB=PI*HT/(PER*SINH(RKD))

C CONVENTIONALLY: AB=HT/(2*SINH(2*PI*D/WL))
C THE MORE EFFICIENT ALGORITH IS:
      AB=UB/W
C---------------------------------------------------------------------

 30   RETURN
      END

C *****************************************************
C SUBROUTINE THRESHOLD
C *****************************************************
      SUBROUTINE THRESH(VISK,GD,RHOS,RHOW,FALL,USTCRB,USTCRS,USTUP,DX)

      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C  THIS SUBROUTINE CALCULATES THE THRESHOLD SHEAR VELOCITIES FOR BEDLOAD,
C  SUSPENDED LOAD, AND SHEET FLOW SEDIMENT TRANSPORT. THE CRITICAL STRESS
C  FOR BEDLOAD TRANSPORT IS BASED ON THE VAN RIJN METHOD.
C  THE CRITICAL STRESS FOR SUSPENDED LOAD IS BASED ON THE VAN RIJN, WHERE
C  THE PARTICLE FALL VELOCITY IS AS GIVEN BY SOULSBY. THE SHEET FLOW
C  CRITICAL SHEAR STRESS IS GIVEN BY LEE AND AMOS.
C
C INPUT VARIABLES:
C     VISK = KINEMATIC VISCOSITY OF FLUID (M**2/SEC)
C       GD = SEDIMENT GRAIN SIZE (M)                (INDATA96 file)
C     RHOS = SEDIMENT DENSITY (KG/M**3)             (READIN subroutine)
C     RHOW = FLUID DENSITY (KG/M**3)                (READIN subroutine)
C
C OUTPUT VARIABLES:
C     FALL = FALL VELOCITY OF SEDIMENT GRAINS AS GIVEN BY SOULSBY (M/SEC)
C   USTCRB = CRITICAL SHEAR VEL. FOR INIT. OF BEDLOAD  (M/SEC)
C   USTCRS = CRITICAL SHEAR VEL. FOR INIT. OF SUSPENDED (M/SEC)
C    USTUP = CRITICAL SHEAR VEL. FOR INIT. OF UPPER PLANE BED SHEET FLOW (M/SEC)
C       DX = DIMENSIONLESS GRAIN SIZE
C
C INTERMEDIATE VARIABLES:
C        G = ACCELERATION DUE TO GRAVITY (M/SEC**2)
C     VISC = DYNAMIC VISCOSITY OF THE FLUID (KG/M*SEC (OR N.S/M**2))
C     DRHO = SEDIMENT DENSITY - FLUID DENSITY (KG/M**3)
C    YALIN = YALIN PARAMETER
C   GYALIN = LOG10 OF THE YALIN PARAMETER
C      SCR = SHIELDS PARAMETER
C      AUX = AUX VARIABLE

C INITIALIZE CONSTANTS
      G = 9.81
      DRHO = RHOS-RHOW
      AUX = (DRHO*G*GD)/RHOW

C COMPUTE THE DIMENSIONLESS PARTICLE DIAMETER
      DX = GD * (((DRHO/RHOW)*G)/VISK**2.)**(1./3.)

C COMPUTE FALL VELOCITY (Soulsby, 1997)
      FALL = (VISK/GD)*((10.36**2. + 1.049*DX**3.)**0.5 - 10.36)

C-----------------------------------------------------------------
C CALCULATE THRESHOLD SHEAR VELOCITY FOR BEDLOAD TRANSPORT, USTCRB
C-----------------------------------------------------------------
C GET SHIELDS PARAMETER USING YALIN METHOD MODIFIED FROM MILLER ET AL. (1977)

      YALIN=SQRT((DRHO*G*GD**3.)/(RHOW*VISK**2.))
      GYALIN=dLOG10(YALIN)
      IF (YALIN .GE. 3000) THEN
         SCR=0.045
      ELSE IF (YALIN .LE. 100) THEN
         SCR=10.**((0.041*GYALIN**2)-0.356*GYALIN-0.977)
      ELSE
         SCR=10.**(0.132*GYALIN-1.804)
      ENDIF

C CONVERT SHIELDS PARAMETER TO SHEAR VELOCITY
      USTCRB=SQRT(SCR*AUX)

C--------------------------------------------------------------------------
C CALCULATE THRESHOLD SHEAR VELOCITY FOR SUSPENDED LOAD TRANSPORT, USTCRS
C--------------------------------------------------------------------------
C GET THE CRITICAL USTCRS/FALL RATIO for SUSPENSION (Van Rijn, 1984)

      IF(DX.GT.1..AND.DX.LE.10.)THEN
        SCR = 4./DX
      ELSE
        SCR = 0.4
      ENDIF

C COMPUTE THRESHOLD SHEAR VELOCITY
      USTCRS=FALL*SCR

C--------------------------------------------------------------------------
C CALCULATE THRESHOLD SHEAR VELOCITY FOR SHEET FLOW TRANSPORT, USTUP
C--------------------------------------------------------------------------
C CALCULATE SHEETFLOW SHIELDS PARAMETER ACCORDING TO LI AND AMOS
c      SCR = 0.172*(100.*GD)**(-0.376)
      SCR = 0.413*(1000*GD)**(-0.4)

C CONVERT SHIELDS PARAMETER TO SHEAR VELOCITY
      USTUP=SQRT(SCR*AUX)

      RETURN
      END

C **********************************************************
C SUBROUTINE FRICFAC
C **********************************************************
      SUBROUTINE FRICFAC(UZ,Z,CDIR,UB,AB,PER,WDIR,GD,RHINP,
     &RLINP,RHOW,RHOS,USTCRB,USTUP,VISK,Z0,Z0C,FCW,UA,PHIB,U100,
     &PHI100,USTCS,USTWS,USTCWS,USTCWSE,USTCWSB,USTC,USTW,USTCW,
     &DELTACW,RHEIGHT,RLENGTH,RPLCOEF)

      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C  THIS SUBROUTINE CALCULATES THE FRICTION FACTOR, SHEAR VELOCITIES AND
C  RIPPLE DIMENSIONS FOR VARIOUS WAVE AND CURRENT CONDITIONS.

C  CASE A: FOR SI92 DATA     YES
C  CASE B: FOR SI82 DATA     NO   (SEE LINES 160 AND 173)

C  FOR COMBINED FLOW, GRAIN-SIZE ROUGHNESS IS FIRST USED TO OBTAIN THE SKIN-
C  FRICTION SHEAR VELOCITY WHICH IS USED TO CALCULATE THE BEDLOAD ROUGHNESS.
C  THE COMBINED GRAIN AND BEDLOAD ROUGHNESS IS USED TO OBTAIN A TRANSPORT-
C  RELATED SHEAR VELOCITY WHICH IS USED TO COMPUTE RIPPLE GEOMETRY AND
C  DETERMINE IF TRANSPORT IS IN SUSPENSION OR SHEETFLOW MODES.
C
C INPUT VARIABLES:
C         UZ = CURRENT SPEED AT HEIGHT Z (M) ABOVE SEABED (M/SEC)    (INDATA96 file)
C          Z = HEIGHT ABOVE SEABED AT WHICH CURRENT IS MEASURED (M)  (INDATA96 file)
C       CDIR = CURRENT DIRECTION AT 1 M. ABOVE SEABED (AZIMUTH)      (INDATA96 file)
C        PER = WAVE PERIOD (SEC)                                     (INDATA96 file)
C       WDIR = WAVE DIRECTION (AZIMUTH)                              (INDATA96 file)
C         GD = SEDIMENT GRAIN SIZE (M)                               (INDATA96 file)
C      RHINP = INPUT RIPPLE HEIGHT (M)                               (INDATA96 file)
C      RLINP = INPUT RIPPLE LENGTH (M)                               (INDATA96 file)
C       RHOW = DENSITY OF FLUID (WATER)  (KG/M**3)                   (READIN subr)
C       RHOS = DENSITY OF SEDIMENT GRAIN  (KG/M**3)                  (READIN subr)
C         UB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE VELOCITY (M/SEC) (OSCIL subr)
C         AB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE DISPLACEMENT (M) (OSCIL subr)
C     USTCRB = CRITICAL SHEAR VELOCITY FOR BEDLOAD TRANSPORT (M/SEC) (THRESH subr)
C      USTUP = CRITICAL SHEAR VELOCITY FOR INITIATION OF UPPER 
C              PLANE BED SHEET FLOW (M/SEC)                          (THRESH subr)
C       VISK = KINEMATIC VISCOSITY OF FLUID (M**2/SEC)               (THRESH subr)
C
C OUTPUT VARIABLES:
C         Z0 = BED ROUGHNESS LENGTH (M)
C        Z0C = APPARENT BED ROUGHNESS LENGTH (M)
C        FCW = BOTTOM FRICTION FACTOR
C         UA = CURRENT SPEED AT THE TOP OF THE WAVE-CURRENT BOUNDARY LAYER 
C              TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
C       PHIB = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS WITHIN THE
C              WAVE BOUNDARY LAYER (RADIANS)
C       U100 = CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
C     PHI100 = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M.
C              ABOVE SEABED (RADIANS)
C                  NOTE:  PHI100 = PHIZ AS LONG AS PHIZ IS MEASURED
C                  OUTSIDE THE WAVE BOUNDARY LAYER.
C      USTCS = CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
C      USTWS = WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
C     USTCWS = COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
C    USTCWSE = EFFECTIVE COMBINED SKIN-FRICTION SHEAR VELOCITY 
C    USTCWSB = TRANSPORT-RELATED COMBINED SHEAR VELOCITY
C       USTC = TOTAL CURRENT SHEAR VELOCITY OF GM 
C       USTW = TOTAL WAVE SHEAR VELOCITY OF GM
C      USTCW = TOTAL COMBINED SHEAR VELOCITY OF GM 
C    DELTACW = HEIGHT OF THE WAVE-CURRENT BOUNDARY LAYER
C    RHEIGHT = PREDICTED RIPPLE HEIGHT (M)
C    RLENGTH = PREDICTED RIPPLE LENGTH (M)
C    RPLCOEF = RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION 
C
C INTERMEDIATE VARIABLES:
C        RKB = BOTTOM ROUGHNESS HEIGHT (M)
C      DELTA = USTCS CONVERGENCE CRITERION FACTOR
C        FWS = SKIN-FRICTION FACTOR
C       FBAD = TOTAL FRICTION FACTOR INCLUDING FORM DRAG
C       UBAD = CURRENT SPEED NEGLECTING FORM DRAG (M/SEC)
C     PHIBAD = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS, WITHIN
C              WAVE B.L. AND NEGLECTING FORM DRAG (RADIANS)
C      RATIO = UA/UB;  DETERMINES VALIDITY OF EQUATION OF MOTION
C              USED BY GRANT AND MADSEN (1979)
C        SKB = PREDICTED GRAIN + BEDLOAD ROUGHNESS HEIGHT
C        BKB = PREDICTED BEDLOAD ROUGHNESS HEIGHT
C        HTM = BEDLOAD LAYER HEIGHT

C INITIALIZE PARAMETERS
        PI=2.*ASIN(1.)
        FCW=0.0
        U100=0.0
        PHI100=0.
        PHIB=0.
        USTCS=0.0
        USTWS=0.0
        USTCWS=0.0
        USTC=0.0
        USTW=0.0
        USTCW=0.0
        USTCWSE=0.0
        USTCWSB=0.0
        Z0=0.0
        Z0C=0.0
        DELTACW=0.0
        RHEIGHT=0.0
        RLENGTH=0.0
        RPLCOEF=1.
        BKB=0.0
        SKB=0.0

C*********************
C PURE CURRENT CASE
C*********************
      IF (UB .LT. 0.01) THEN

C GET PRELIMINARY BED ROUGHNESS HEIGHT RKB ACCORDING TO GRANT AND MADSEN
C (1982). IF THERE IS NO MEASUREMENT OF RIPPLE HEIGHTS, RKB=2.5*GD, OTHERWISE
C USE THE MEASURED INPUT RIPPLE HEIGHT AND LENGTH TO OBTAIN RKB=27.7*RHINP
C *RHINP/RLINP:
        IF (RHINP .EQ. 0) THEN
           RKB=2.5*GD
        ELSE
           RKB=27.7*RHINP*RHINP/RLINP
        ENDIF

C CALL FRIC1 TO OBTAIN "FCW" AND THE PRELIMINARY ESTIMATES OF "UA","U100","USTC"
        CALL FRIC1(UZ,Z,GD,RKB,FCW,UA,U100,USTC)

C COMPUTE PRELIMINARY SKIN-FRICTION CURRENT SHEAR VELOCITY
        USTCS=SQRT(0.5*FCW*U100**2)

C ASSIGN AN ARBITRARY VALUE TO THE USTCS CONVERGENCE CRITERION DELTA
        DELTA=1.0
 100    IF (DELTA .LT. 0.0001) GOTO 200

C PREDICT CURRENT RIPPLE DIMENSIONS:
C FOR NO-TRANSPORT CASE OR COARSE AND VERY-COARSE SANDS, BEDFORM DIMENSION
C WILL NOT BE PREDICTED AND INPUT VALUES WILL BE USED. OTHERWISE, RIPPLE
C HEIGHT AND LENGTH WILL BE PREDICTED ACCORDING TO YALIN (1964) AND ALLEN
C (1970), RESPECTIVELY.
        IF (USTCS .LT. USTCRB .OR. GD .GE. 0.0005) THEN
           RHEIGHT=RHINP
           RLENGTH=RLINP
        ELSE

C FOR SHEET-FLOW, RIPPLE HEIGHT AND LENGTH WILL BE 0
           IF (USTCS .GE. USTUP) THEN
              RHEIGHT=0
              RLENGTH=0
           ELSE
              RLENGTH=1000*GD
              RHEIGHT=0.00074*(100*RLENGTH)**1.19
          ENDIF

        ENDIF
        IF (RHEIGHT .EQ. 0) THEN
           USTC=USTCS
           RKB=30*EXP(LOG(Z)-0.4*UZ/USTC)
        ELSE
           RKB=27.7*RHEIGHT*RHEIGHT/RLENGTH
        ENDIF

C CALL FRIC1 WITH NEW RKB TO OBTAIN NEW U100 AND USTC
        CALL FRIC1(UZ,Z,GD,RKB,FCW,UA,U100,USTC)

C ASSIGN THE INITIAL USTCS TO A TEMPORARY VARIABLE
        USTCSP=USTCS

C COMPUTE THE NEW SKIN-FRICTION CURRENT SHEAR VELOCITY
        USTCS=SQRT(0.5*FCW*U100**2)

C OBTAIN THE USTCS CONVERGENCE CRITERION DELTA
        DELTA=DABS(USTCSP/USTCS-1)

        IF (USTCSP.EQ.0..AND.USTCS.EQ.0.)THEN
          DELTA=0.00001
        ENDIF

C GOTO 100 TO COMPARE THE DELTA VALUE WITH THE SET CRITERION VALUE
        GOTO 100

 200    CONTINUE

C GET FINAL BED ROUGHNESS Z0
        Z0=RKB/30

        RETURN

      ENDIF

C*************************
C WAVES AND CURRENT CASE  
C*************************

      IF (UZ .NE. 0.0) THEN

      PHI100=DMIN1(ABS(CDIR-WDIR),ABS(180.-ABS(CDIR-WDIR)),
     @         360.-ABS(CDIR-WDIR))*ASIN(1.)/90.
      PHIB=PHI100

C COMPUTE SKIN-FRICTION "FCW" AND "UST'S" BASED ON GRAIN ROUGHNESS HEIGHT GKB ONLY
      GKB = 2.5*GD
      CALL FRIC2(UZ,Z,PHI100,UB,PER,GKB,RKBC,FCW,UA,PHIB,
     @U100,USTCS,USTWS,USTCWS,DELTACW)

C COMPUTE INITIAL BED ROUGHNESS HEIGHTS BASED ON SKIN-FRICTION UST'S
      CALL ROUGH(VISK,RHINP,RLINP,USTCS,USTWS,USTCWS,USTCRB,
     @USTUP,GD,RHOW,RHOS,RHEIGHT,RLENGTH,SKB,HTM,TKB,USTCWSE,
     @RPLCOEF)

      BKB=180*HTM
      SKB=2.5*GD+BKB

C COMPUTE TRANSPORT-RELATED FCWB AND UST'S USING THE SUM OF GRAIN AND BEDLOAD
C ROUGHNESS HEIGHTS, SKB.
      CALL FRIC2(UZ,Z,PHI100,UB,PER,SKB,RKBC,FCWB,UA,PHIB,
     @U100,USTCSB,USTWSB,USTCWSB,DELTACW)

C COMPUTE FINAL BED ROUGHNESS HEIGHTS BASED ON TRANSPORT-RELATED UST'S.
C USE PREDICTED RIPPLE GEOMETRY FROM ROUGH AS INPUTS IF THE RIPPLE-ENHANCED
C SHEAR VELOCITY U*CWSE IS LARGER THAN BEDLOAD THRESHOLD SHEAR VELOCITY U*CRB

      CALL ROUGH(VISK,RHINP,RLINP,USTCSB,USTWSB,USTCWSB,USTCRB,
     @USTUP,GD,RHOW,RHOS,RHEIGHT,RLENGTH,SKB,HTM,TKB,USTCWSE,
     @RPLCOEF)

C COMPUTE TOTAL FCW (FBAD) AND UST'S BASED ON TOTAL ROUGHNESS HEIGHT TKB.
      CALL FRIC2(UZ,Z,PHI100,UB,PER,TKB,RKBC,FBAD,UA,PHIB,
     @U100,USTC,USTW,USTCW,DELTACW)

C OBTAIN BED ROUGHNESS Z0, APPARENT BED ROUGHNESS Z0C
      Z0=TKB/30
      Z0C=RKBC

C PREVENT FRICTION FACTOR FROM FALLING BELOW THE 'CURRENT ONLY' VALUE
C FROM STERNBERG (1972)
      IF(FCW.LT.0.006D0)FCW=0.006D0

      RETURN
      ENDIF

C******************
C PURE WAVES CASE
C******************
C CALL FRIC3 USING MAX WAVE-INDUCED BOTTOM PARTICLE VELOCITY "AB"
C AND GRAIN SIZE "GD" TO COMPUTE SKIN-FRICTION FWS AND U*WS
      CALL FRIC3(AB,GD,FCW)
      UA=0.0
      W=2.*PI/PER
      USTWS=SQRT(0.5*FCW*UB**2)

C PREDICT WAVE RIPPLE DIMENSIONS
C FOR NO-TRANSPORT CASE OR COARSE AND VERY-COARSE SANDS, BEDFORM DIMENSION
C WILL NOT BE PREDICTED AND INPUT VALUES WILL BE USED
      IF (USTWS .LT. USTCRB .OR. GD .GE. 0.0005) THEN
         RHEIGHT=RHINP
         RLENGTH=RLINP
      ELSE

C FOR FINE OR MEDIUM SAND IN ACTIVE TRANSPORT, RIPPLES ARE PREDICTED
C ACCORDING TO BOYD ET AL. (1988) AND ALLEN (1970)
         IF (USTWS .GE. USTUP) THEN
            RHEIGHT=0
            RLENGTH=0
         ELSE
            RLENGTH=AB*557*(UB*AB/VISK)**(-0.68)
            RHEIGHT=0.00074*(100*RLENGTH)**1.19
         ENDIF
      ENDIF

      IF (RLENGTH .EQ. 0.) THEN
         RKB=2.5*GD
      ELSE
         RKB=27.7*RHEIGHT*RHEIGHT/RLENGTH
      ENDIF

C CALL FRIC3 AGAIN USING NEW ROUGHNESS HEIGHT TO COMPUTE THE TOTAL FW AND U*W
      CALL FRIC3(AB,RKB,FCW)
      USTW=SQRT(0.5*FCW*UB**2)
      DELTACW=2.*0.4*USTW/W
      Z0=RKB/30

      RETURN
      END

C**************************************************************************
C**************************************************************************
      SUBROUTINE FRIC1(UZ,Z,GD,RKB,FCW,UA,U100,USTC)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C  THIS SUBROUTINE CALCULATES THE BOTTOM FRICTION FACTOR FOR THE PURE
C  CURRENT CASE. A CONSTANT FRICTION FACTOR IS ASSUMED, BASED ON THE
C  WORK OF STERNBERG (1971).
C
C  INPUT VARIABLES:
C         UZ = CURRENT SPEED AT HEIGHT Z (M) ABOVE SEABED (M/SEC)
C          Z = HEIGHT ABOVE SEABED AT WHICH CURRENT IS MEASURED (M)
C         GD = SEDIMENT GRAIN SIZE (M)
C        RKB = BOTTOM ROUGHNESS (M)
C
C  OUTPUT VARIABLES:
C        FCW = BOTTOM FRICTION FACTOR FOR THE PURE CURRENT CASE
C         UA = CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
C       U100 = CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
C       USTC = TOTAL CURRENT SHEAR VELOCITY OF GM (M/SEC)

      FCW=6.0E-3
      IF(RKB .EQ. 0.0) RKB=2.5*GD
      USTC=0.4*UZ/LOG(30*Z/RKB)
      U100=(USTC/0.4)*LOG(1*30/RKB)
      UA=U100
      RETURN
      END

C**************************************************************************
C**************************************************************************
      SUBROUTINE FRIC2(UZ,Z,PHI100,UB,PER,RKB,RKBC,FCW,UA,PHIB,
     @U100,USTC,USTW,USTCW,DELTACW)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C  THIS SUBROUTINE CALCULATES THE FRICTION FACTOR FOR COMBINED WAVE AND
C  CURRENT CONDITIONS USING THE METHOD OF GRANT AND MADSEN (1986). THIS
C  METHOD IS NOT VALID FOR UA/UB > 1.0 (APPROXIMATELY) DUE TO THE REL-
C  ATIVE IMPORTANCE OF THE CONVECTIVE ACCELERATION TERMS IN THE EQUATION
C  OF MOTION.
C
C  INPUT VARIABLES:
C         UZ = CURRENT SPEED AT HEIGHT Z (M) ABOVE SEABED (M/SEC)
C     PHI100 = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M.
C              ABOVE SEABED (RADIANS)  (NB:  PHI100 = PHIZ)
C         UB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE VELOCITY (M/SEC)
C        PER = WAVE PERIOD (SEC)
C        RKB = BOTTOM ROUGHNESS HEIGHT (M)
C
C  OUTPUT VARIABLES:
C        FCW = BOTTOM FRICTION FACTOR FOR THE COMBINED CASE
C         UA = CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
C       U100 = CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
C       PHIB = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS WITHIN THE
C              WAVE BOUNDARY LAYER (RADIANS)
C       RKBC = APPARENT BOTTOM ROUGHNESS (M)
C       USTC = CURRENT SHEAR VELOCITY OF GM (M/SEC)
C       USTW = WAVE SHEAR VELOCITY OF GM (M/SEC)
C      USTCW = COMBINED SHEAR VELOCITY OF GM (M/SEC)
C    DELTACW = THICKNESS OF THE WAVE-CURRENT BOUNDARY LAYER
C
C  INTERMEDIATE VARIABLES:
C          W = WAVE ANGULAR FREQUENCY (RAD/SEC)
C         Z0 = DYNAMIC BOTTOM ROUGHNESS LENGTH
C         CR = FACTOR DESCRIBING RELATIVE RATIO OF USTC/USTW
C      DELTA = ITERATION CRITERION FOR UST CALCULATION
C       TEMP = TEMPORARY VARIABLE

C  INITIALIZE ITERATION PARAMETERS
	
      PI=2.*ASIN(1.)
      W=2.*PI/PER
      PHIB=PHI100 
      DELTA=1.0
      CR=1.0
      FCW=0.01

C CALCULATE UST'S BY ITERATION (GM,1986)
 10   IF (DELTA .LT. 0.00001) GO TO 200      
      Z0=RKB/30.
      TEMP=CR*UB/(Z0*W)
C INITIALIZE THE ITERATION CRITERION EPSILON     
      EPSILON=1.0
 50   IF (EPSILON .LT. 0.00001) GO TO 100

C CALCULATE FCW ACCORDING TO GM (1986). NEWTON-RAPHSON SOLUTION IS USED IN      
C ITERATION.
      X=4*SQRT(FCW)
      A=0.24*X-1.65+DLOG10(TEMP*X)-1/X
      B=0.24+1/X**2+1/(X*DLOG(10.0D00))
      DX=A/B
      XNEW=X-DX
      FCWNEW=(XNEW/4)**2
      EPSILON=DABS(FCWNEW/FCW-1)
      FCW=FCWNEW
      GO TO 50
 100  CONTINUE 
C     CALCULATE USTW, USTCW AND DELTACW (BOUNDARY LAYER THICKNESS)     
      USTW=SQRT(0.5*CR*FCW*UB**2)
      USTCW=SQRT(CR)*USTW
      DELTACW=2.*0.4*USTCW/W        !ggu error in article -> must be omega
C---- CALCULATE CURRENT SHEAR VELOCITY USTC ------------------------
C     FOR DELTACW=Z0, NO WAVE EFFECT AND PURE CURRENT ASSUMED
      IF (DELTACW .LE. Z0) THEN
         FCW=6.0E-3
         U100=UZ*LOG(30.0/RKB)/LOG(30.*Z/RKB)
         UA=U100
         PHIB=0.0
         USTC=SQRT(0.5*FCW*U100**2)
         USTCW=USTC
         USTW=0						!ccf INTEL
C GET FINAL BED ROUGHNESS AND BED ROUGHNESS HEIGHT USING LOG LAW
         Z0=EXP(LOG(Z)-UZ*0.4/USTC)
         RKB=30*Z0
         RKBC=RKB
         RETURN
      ENDIF
      AA=DLOG(DELTACW/Z0)/USTCW
      BB=DLOG(Z/DELTACW)
      CC=-0.4*UZ
      DD=SQRT(BB**2-4*AA*CC)
      USTC=0.5*(-BB+DD)/AA
      RKBC=DELTACW*EXP(-AA*USTC)
      CRNEW=SQRT(1.0+2*(USTC/USTW)**2*COS(PHIB)
     @      +(USTC/USTW)**4)
      DELTA=DABS(CRNEW/CR-1)
      CR=CRNEW
      GO TO 10

 200  CONTINUE
C CALCULATE VELOCITY AT THE TOP OF THE WAVE-CURRENT BOUNDARY LAYER     
      UA=(USTC/0.4)*DLOG(DELTACW/RKBC)
C CALCULATE VELOCITY AT 1M ABOVE THE BOTTOM      
      IF (DELTACW .GT. 1.) THEN
        U100=(USTC/USTCW)*(USTC/0.4)*DLOG(1.0D00/Z0)
      ELSE
        U100=(USTC/0.4)*DLOG(1.0D00/RKBC)
      ENDIF

      RETURN
      END

C**************************************************************************
C**************************************************************************
      SUBROUTINE FRIC3(AB,RKB,FCW)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C  THIS SUBROUTINE CALCULATES THE BOTTOM FRICTION FACTOR FOR THE PURE
C  WAVE CONDITION USING THE METHOD OF JONSSON (1966) AS MODIFIED BY
C  NIELSEN (1979).  THE BOTTOM ROUGHNESS IS TAKEN AS THE GRAIN DIAMETER
C  AS IN GRANT AND MADSEN (1976).
C
C  INPUT VARIABLES:
C         AB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE DISPLACEMENT (M)
C        RKB = BOTTOM ROUGHNESS HEIGHT (M)
C
C  OUTPUT VARIABLES:
C        FCW = BOTTOM FRICTION FACTOR FOR THE PURE WAVE CASE

      FCW=DMIN1(EXP(5.213*(RKB/AB)**0.194-5.977),0.28D0)
      RETURN
      END

C**************************************************************************
C**************************************************************************
      SUBROUTINE ROUGH(VISK,RHINP,RLINP,USTCS,USTWS,USTCWS,USTCRB,
     @USTUP,GD,RHOW,RHOS,RHEIGHT,RLENGTH,SKB,HTM,TKB,USTCWSE,
     @RPLCOEF)

      IMPLICIT DOUBLE PRECISION(A-H,K,O-Z)

C  THIS SUBROUTINE USES SKIN-FRICTION SHEAR VELOCITIES TO CALCULATE THE
C  INITIAL RIPPLE GEOMETRY AND VARIOUS ROUGHNESS HEIGHTS BASED ON THE GM86
C  COMBINED-FLOW BBL MODEL AND THE LATEST SIB DATA
C
C  INPUT VARIABLES
C      VISK = KINEMATIC VISCOSITY OF FLUID (M**2/SEC)
C     RHINP = INITIAL RIPPLE HEIGHT (M)         
C     RLINP = INITIAL RIPPLE LENGTH (M)
C     USTCS = GM CURRENT SKINFRICTION SHEAR VELOCITY (M/S)
C     USTWS = GM WAVE SKINFRICTION SHEAR VELOCITY (M/S)
C    USTCWS = GM COMBINED SKINFRICTION SHEAR VELOCITY (M/S)
C    USTCRB = CRITICAL SHEAR VELOCITY FOR THE INITIATION OF BEDLOAD TRANSPORT
C     USTUP = CRITICAL SHEAR VELOCITY FOR UPPER PLANE BED
C        GD = SEDIMENT GRAIN SIZE
C      RHOW = FLUID DENSITY; RHOS=SEDIMENT GRAIN DENSITY
C      RHOS = SEDIMENT DENSITY; 
C    
C  OUTPUT VARIABLES
C    RHEIGHT = PREDICTED RIPPLE HEIGHT
C    RLENGTH = PREDICTED RIPPLE LENGTH
C        SKB = PREDICTED GRAIN + BEDLOAD ROUGHNESS HEIGHT
C        HTM = BEDLOAD LAYER HEIGHT
C        TKB = PREDICTED TOTAL BOTTOM ROUGHNESS HEIGHT
C    USTCWSE = EFFECTIVE SHEAR VELOCITY AT THE RIPPLE CREST
C    RPLCOEF = UST CONVERSION COEFFICIENT
C
C  INTERMEDIATE VARIABLES
C          G = GRAVITY ACCELERATION
C        SST = DIMENTIONLESS SEDIMENT GRAIN SIZE PARAMETER
C      PSICR = CRITICAL SHIELDS PARAMETER FOR BEDLOAD TRANSPORT
C      PSIBF = GM82 CRITICAL SHIELDS PARAMETER FOR RIPPLE BREAKOFF
C      USTBF = CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
C        GKB = GRAIN ROUGHNESS HEIGHT (=2.5*GD)
C      STEEP = PREDICTED RIPPLE STEEPNESS

C INITIALIZING PARAMETERS
      G=9.81                     
      S=RHOS/RHOW
      SST=GD*((S-1)*G*GD)**0.5/(4*VISK)
      PSICR=(USTCRB**2)/((S-1)*G*GD)
      PSIBF=1.8*(SST**0.6)*PSICR
      USTBF=(PSIBF*(S-1)*G*GD)**0.5

C-----------------------------------------------------------------
C  FOR COARSE AND VERY-COARSE SANDS, BEDFORM GEOMETY CANNOT BE PREDICTED
C  AND THE INITIAL RIPPLE HEIGHT AND LENGTH WILL BE USED
      IF(GD .GE. 0.0005) THEN
           RHEIGHT=RHINP
           RLENGTH=RLINP
           IF (RLENGTH .EQ. 0) STEEP = 0
           IF (RLENGTH .GT. 0) STEEP=RHEIGHT/RLENGTH
           GO TO 33
      ENDIF

C-----------------------------------------------------------------
C  COMPUTE THE INITIAL RIPPLE CONVERSION COEFFICIENT RPLCOEF. IT IS EQUAL 
C  TO 1 FOR FLAT BED, OTHERWISE IS CALCULATED ACCORDING TO NIELSEN (1992)
      IF(RHINP .EQ. 0 .OR. USTCWS .GE. USTUP) THEN
           RPLCOEF=1
      ELSE
           RPLCOEF=1/(1-3.14*RHINP/RLINP)
      ENDIF

C  COMPUTE EFFECTIVE SHEAR VELOCITY AT THE RIPPLE CREST
      USTCWSE=USTCWS*RPLCOEF

C-----------------------------------------------------------------
C  FOR NO TRANSPORT CASE, USE THE COMBINED WAVE-CURRENT RIPPLE HEIGHT AND
C  LENGTH AT THE BEDLOAD THRESHOLD CONDITION BASED ON LI AND AMOS (IN REVIEW)
C      IF(USTCWSE .LT. USTCRB) THEN
C           STEEP=0.12
C           RHEIGHT=GD*(22.15+6.38)
C           RLENGTH=RHEIGHT/STEEP
C           HTM=0
C           GO TO 33
C      ENDIF

C----------------------------------------------------------------- !ggu check
C  USE THE INITIAL RIPPLE HEIGHT AND LENGTH FOR NO TRANSPORT CASE  !ggu Li96
       IF(USTCWSE .LT. USTCRB) THEN
            RHEIGHT=RHINP
            RLENGTH=RLINP
            IF (RLENGTH .EQ. 0) STEEP = 0
            IF (RLENGTH .GT. 0) STEEP=RHEIGHT/RLENGTH
            HTM=0
            GO TO 33
       ENDIF

C-----------------------------------------------------------------
C  COMPUTE RIPPLES FOR ACTIVE TRANSPORT CASE

C  RIPPLES IN THE WEAK-TRANSPORT RANGE
      IF(USTCWSE .GE. USTCRB .AND. USTCWS .LT. USTCRB) THEN
           STEEP=0.11
           RHEIGHT=GD*(19.59*(USTCWS/USTCRB)+20.92)
           RLENGTH=RHEIGHT/STEEP
      ENDIF

C  RIPPLES IN THE EQUILIBRIUM RANGE
      IF(USTCWS .GE. USTCRB .AND. USTCWS .LT. USTBF) THEN
C  COMPUTE WAVE-DOMINANT RIPPLES FOR U*WS/U*CS>=1.25
           IF(USTWS/USTCS .GE. 1.25) THEN
                STEEP=0.15
c                RHEIGHT=GD*(9.86*(USTCWS/USTCRB)+37.91)
                RHEIGHT=GD*(27.14*(USTCWS/USTCRB)+16.36)
           ELSE
C  COMPUTE COMBINED WAVE-CURRENT OR CURRENT-DOMINANT RIPPLES
                STEEP=0.12
                RHEIGHT=GD*(22.15*(USTCWS/USTCRB)+6.38)
           ENDIF
           RLENGTH=RHEIGHT/STEEP
      ENDIF

C  COMPUTE RIPPLES IN THE BREAK-OFF RANGE BASED ON THE SIB METHOD
      IF(USTCWS .GE. USTBF .AND. USTCWS .LT. USTUP) THEN
             RLENGTH = 530*GD
             STEEP=(0.15/(1-USTBF/USTUP))*(1-USTCWS/USTUP)
             RHEIGHT = RLENGTH*STEEP
      ENDIF

C  NO RIPPLES UNDER SHEET FLOW CONDITIONS
      IF (USTCWS .GE. USTUP) THEN
           RHEIGHT=0
           RLENGTH=0
           STEEP=0
      ENDIF

 33   CONTINUE     

C  COMPUTE THE TRUE RIPPLE CONVERSION COEFFICIENT RPLCOEF
      IF(RHEIGHT .EQ. 0 .OR. RLENGTH .EQ. 0) THEN
           RPLCOEF=1
      ELSE
           RPLCOEF=1/(1-3.14*RHEIGHT/RLENGTH)
      ENDIF

C ************************************
C THE NEXT LINE IS ONLY RUN FOR SIB82 DATA TO CALCULATE RPLCOEF USING
C INPUT RIPPLE HEIGHT AND LENGTH
C      RPLCOEF=1/(1-3.14*RHINP/RLINP)

C  COMPUTE EFFECTIVE SHEAR VELOCITY AT THE RIPPLE CREST
      USTCWSE=USTCWS*RPLCOEF

C  CALCULATE BEDLOAD LAYER HEIGHT HTM USING THE MODIFIED NIELSEN 
C  (1992) METHOD
      IF(USTCWS .GE. USTCRB) THEN
           HTM=2.9*GD*((USTCWS**2)/((S-1)*G*GD)-PSICR)**0.75
C  LIMIT HTM TO BE <10*GRAIN DIAMETER
           IF (HTM .GT. 10*GD) HTM=10*GD
C  CALCULATE BEDLOAD LAYER HEIGHT HTM USING THE MODIFIED GM82 METHOD
C           HTM=1.22*GD*(S+0.5)*PSICR*(USTCWS/USTCRB-0.7)**2
C      ELSE IF(USTCWSE .GE. USTCRB) THEN
C           HTM=1.22*GD*(S+0.5)*PSICR*((USTCRB/USTCWS)**(0.282)*
C     @         USTCWS/USTCRB-0.7)**2

      ELSE
           HTM=0
      ENDIF

C  COMPUTE VARIOUS ROUGHNESS HEIGHTS: RKB, RIPPLE ROUGHNESS HEIGHT
C  AND TKB, TOTAL BED ROUGHNESS HEIGHT.
      RKB=27.7*RHEIGHT*STEEP
      TKB=RKB+SKB

      RETURN
      END

C***********************************************************************
C*******************************************************************
C SUBROUTINE TIMIMG
C*******************************************************************

      SUBROUTINE TIMING(RPLCOEF,RHOW,UA,PHIB,UB,PER,U100,USTCRB,
     &USTCRS,USTCS,USTWS,USTCWS,USTCWSB,TB1,TB2,TS1,TS2,PERBED,
     &PERSUSP)

      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C  TIMING2.FOR USES EFFECTIVE SHEAR STRESSES:
C      CASE 0 - AVERAGE SHEAR STRESS (LINE 92)                   NO
C      CASE A - EFFECTIVE SHEAR STRESSES (LINE 102)             YES
C      CASE B - MAXIMIZED EFFECTIVE SHEAR STRESSES (LINE 114)    NO
C
C  THIS SUBROUTINE USES EFFECTIVE SHEAR STRESSES AT RIPPLE CREST TO
C  CALCULATE THE DURATION OF SEDIMENT TRANSPORT PHASES (NO TRANSPORT, BEDLOAD
C  TRANSPORT, SUSPENDED LOAD TRANSPORT) BY CALCULATING WHEN THE RESPECTIVE
C  CRITICAL SHEAR STRESSES ARE EXCEEDED.
C  ** BEDLOAD USTCWSB IS 'NOT' USED IN DETERMINING TIME FOR SUSPENDED LOAD
C  TRANSPORT
C
C INPUT VARIABLES:
C       PER = WAVE PERIOD (SEC)
C      PHIB = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS WITHIN THE
C             WAVE BOUNDARY LAYER (RADIANS)
C   RPLCOEF = RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
C      U100 = CURRENT VELOCITY AT 1M OF HEIGHT
C        UA = CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
C        UB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE VELOCITY (M/SEC)
C    USTCRB = CRITICAL FLUID VELOCITY FOR INITIATION OF BEDLOAD
C             TRANSPORT (M/SEC)
C    USTCRS = CRITICAL FLUID VELOCITY FOR INITIATION OF SUSPENDED
C             LOAD TRANSPORT (M/SEC)
C   USTCS = CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
C   USTWS = WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
C  USTCWS = COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
C
C  OUTPUT VARIABLES:
C    PERBED = PERCENTAGE OF TIME SPENT IN ONLY BEDLOAD TRANSPORT PHASE.
C   PERSUSP = PERCENTAGE OF TIME SPENT IN SUSPENDED LOAD TRANSPORT PHASE.
C       TS1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH SUSPENDED
C             LOAD TRANSPORT CEASES (SEC)
C       TB1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD
C             TRANSPORT CEASES (SEC)
C       TS2 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH SUSPENDED
C             LOAD TRANSPORT RECOMMENCES (SEC)
C       TB2 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD
C             TRANSPORT RECOMMENCES (SEC)
C     USTCS = MAXIMUM CURRENT SKIN-FRICTION SHEAR VELOCITY
C     USTWS = GM WAVE SKIN-FRICTION SHEAR VELOCITY
C    USTCWS = GM WAVE-CURRENT SKIN-FRICTION SHEAR VELOCITY
C   USTCWSE = RIPPLE-ENHANCED EFFECTIVE SKIN-FRICTION SHEAR VELOCITY
C
C  INTERMEDIATE VARIABLES:
C    USTCSE = RIPPLE-ENHANCED EFFECTIVE CURRENT SKIN-FRICTION SHEAR VELOCITY
C    USTWSE = RIPPLE-ENHANCED EFFECTIVE WAVE SKIN-FRICTION SHEAR VELOCITY
C       XS1 = B+SQRT(B24ACS) USED IN SUSPENDED TIME CALCULATION UNDER CREST
C       XB1 = B+SQRT(B24ACB) USED IN BEDLOAD TIME CALCULATION UNDER CREST
C       XS2 = B-SQRT(B24ACS) USED IN SUSPENDED TIME CALCULATION UNDER TROUGH
C       XB2 = B-SQRT(B24ACB) USED IN BEDLOAD TIME CALCULATION UNDER TROUGH
C         B = -B/2A, AS IN EQ'N. FOR ROOTS OF A QUADRATIC EQUATION
C     B24AC = (B**2-4*A*C)/(2*A)**2, AS IN QUADRATIC EQ'N. SOLUTION

C  FIRST, SET DEFAULT VALUES
      PI=2.*ASIN(1.)
      TS1=0.0
      TB1=0.0
      TS2=0.0
      TB2=0.0
      PERSUSP=0.0
      PERBED=0.0

C PRESERVE THE BURST-AVERAGED SHEAR VELOCITIES
      USTCSGM=USTCS
      USTWSGM=USTWS
      USTCWSGM=USTCWS

C*****************************
C PURE CURRENT CASE
C*****************************
      IF (UB .LT. 0.01) THEN
        IF (USTCS .GE. USTCRS) PERSUSP=100
        IF (USTCS .GE. USTCRB .AND. USTCS .LT. USTCRS) PERBED=100
        RETURN
      ENDIF

C*****************************
C WAVE PRESENT CASE
C*****************************
C --- CASE 0: NO (USE AVERAGE SHEAR STRESS)
C OBTAIN GRANT-MADSEN SKIN-FRICTION CURRENT SHEAR VELOCITY
C      USTCSE=USTCS
C OBTAIN SKIN-FRICTION WAVE SHEAR VELOCITY
C      USTWSE=USTWS
C OBTAIN SKIN-FRICTION COMBINED SHEAR VELOCITY
C      USTCWSE=USTCWS
C ASIGN THE COMBINED SHEAR VELOCITY TO THE MAXIMUM SHEAR VELOCITY
C      USTCWSM=USTCWSE

C --- CASE A: YES (USE RIPPLE-ENHANCED AVEARGE SHEAR STRESSES)
C COMPUTE RIPPLE-ENHANCED EFFECTIVE SKIN-FRICTION CURRENT SHEAR VELOCITY
      USTCSE=USTCS*RPLCOEF
C COMPUTE EFFECTIVE SKIN-FRICTION WAVE SHEAR VELOCITY
      IF (UB .NE. 0.0) USTWSE=USTWS*RPLCOEF
C COMPUTE THE EFFECTIVE SKIN-FRICTION COMBINED SHEAR VELOCITY
      USTCWSE=USTCWS*RPLCOEF
C ASIGN EFFECTIVE COMBINED SHEAR VELOCITY TO THE MAXIMUM SHEAR VELOCITY
c      USTCWSM=USTCWSE

C --- CASE B: NO (USE MAXIMIZED SHEAR STRESSES FOR CURRENT DOMINANT
C                 OR WAVE-CURRENT COMBINED CONDITIONS)
C      IF (USTWS/USTCS .LT. 1.25) THEN
C CHOOSE THE BIGGER FROM THE GM AND STERNBERG CURRENT SHEAR VELOCITIES
C      IF (USTCS .LT. USTCS1) USTCS=USTCS1
C          TAOCS=RHOW*USTCS**2
C          TAOWS=RHOW*USTWS**2
C COMPUTE VECTORIALLY THE MAXIMIZED COMBINED SKIN-FRICTION SHEAR STRESS BASED
C ON THE GM TAOWS AND MAXIMIZED TAOCS
C          TAOCWS=SQRT((TAOCS*SIN(PHIB))**2+
C     @            (TAOCS*COS(PHIB)+TAOWS)**2)
C          USTCWS=SQRT(TAOCWS/RHOW)
C      ENDIF
C COMPUTE RIPPLE-ENHANCED EFFECTIVE SKIN-FRICTION CURRENT SHEAR VELOCITY
C      USTCSE=USTCS*RPLCOEF
C COMPUTE EFFECTIVE SKIN-FRICTION WAVE SHEAR VELOCITY
C      IF (UB .NE. 0.0) USTWSE=USTWS*RPLCOEF
C COMPUTE THE EFFECTIVE SKIN-FRICTION COMBINED SHEAR VELOCITY
C      USTCWSE=USTCWS*RPLCOEF
C ASIGN EFFECTIVE COMBINED SHEAR VELOCITY TO THE MAXIMUM SHEAR VELOCITY
C      USTCWSM=USTCWSE

C-------------------------
C FOR WAVE-ONLY CONDITION
C-------------------------
      IF (UA .EQ. 0.0) THEN
C COVERT RIPPLE-ENHANCED SHEAR VELOCITIES TO SHEAR STRESSES
         TAOWS=RHOW*USTWSE**2
         TAOCRB=RHOW*USTCRB**2
         TAOCRS=RHOW*USTCRS**2
C CHECK IF SAND SUSPENSION OCCURS
         IF (USTCRS .LT. USTWSE) THEN
            TS1=PER/(2.*PI)*ACOS(TAOCRS/TAOWS)
            TS2=PER/2.-TS1
            PERSUSP=400.*TS1/PER
         ENDIF
C CHECK IF ONLY BEDLOAD TRANSPORT OCCURS AT TIMES
         IF (USTCRB .LT. USTCRS .AND. USTCRB .LT. USTWSE) THEN
            TB1=PER/(2.*PI)*ACOS(TAOCRB/TAOWS)
            TB2=PER/2.-TB1
            PERBED=400.*(TB1-TS1)/PER
         ENDIF
         RETURN
      ENDIF

C----------------------------------------
C CONSIDER COMBINED WAVES AND CURRENTS.
C----------------------------------------
C COVERT RIPPLE-ENHANCED SHEAR VELOCITIES TO SHEAR STRESSES
      TAOCS=RHOW*USTCSE**2
      TAOWS=RHOW*USTWSE**2
      TAOCRB=RHOW*USTCRB**2
      TAOCRS=RHOW*USTCRS**2

C*** FIRST CALCULATE TIMES FOR SUSPENDED LOAD ***
      IF (USTCWSE .LT. USTCRS) THEN
         PERSUSP=0
         GO TO 50
      ENDIF
      B24ACS=(TAOCRS**2.0-(TAOCS*SIN(PHIB))**2.0)/(TAOWS**2.0)
      IF (B24ACS .LE. 0.0) THEN
C CRITICAL STRESS FOR SUSPENSION OF SEDIMENT ALWAYS EXCEEDED
         TS1=PER/2.
         PERSUSP=100.0
         PERBED=0.0
         RETURN
      ENDIF
      B=-TAOCS*COS(PHIB)/TAOWS
      XS1=B+SQRT(B24ACS)
      IF (XS1 .GE. 1.0) THEN
C CRITICAL STRESS FOR SUSPENSION OF SEDIMENT NEVER EXCEEDED
         PERSUSP=0.0
         GO TO 50
      ENDIF
      IF (XS1 .LE. -1.0) THEN
C SECOND CASE WHERE CRITICAL STRESS FOR SUSPENSION OF SEDIMENT IS
C ALWAYS EXCEEDED
         TS1=PER/2.
         PERSUSP=100.0
         PERBED=0.0
         RETURN
      ELSE
C CRITICAL STRESS FOR SUSPENSION OF SEDIMENT SOMETIMES EXCEEDED
         TS1=PER/(2.*PI)*ACOS(XS1)
      ENDIF
      XS2=B-SQRT(B24ACS)
      IF (XS2 .LE. -1.0) THEN
C CRITICAL STRESS FOR SUSPENSION OF SEDIMENT NOT EXCEEDED DURING TROUGH
         PERSUSP=200.*TS1/PER
      ELSE
C CRITICAL STRESS FOR SUSPENSION OF SEDIMENT EXCEEDED DURING TROUGH
         TS2=PER/(2.*PI)*ACOS(XS2)
         PERSUSP=(2.*(TS1-TS2)+PER)/PER*100.
      ENDIF

C**** CALCULATE TIMES FOR BEDLOAD ****
C CALCULATE TIMES FOR BEDLOAD ONLY IF USTCRB < USTCRS
  50  IF (USTCRB .GE. USTCRS ) RETURN

C FOR EFFECTIVE SHEAR VELOCITY < USTCRB, NO BEDLOAD TRANSPORT
      IF (USTCWSE .LT. USTCRB) THEN
         PERBED=0
         RETURN
      ENDIF
      B24ACB=(TAOCRB**2-(TAOCS*SIN(PHIB))**2)/(TAOWS**2)
      IF (B24ACB .LE. 0.0) THEN
C CRITICAL STRESS FOR BEDLOAD TRANSPORT ALWAYS EXCEEDED
         TB1=PER/2.
         PERBED=100.-PERSUSP
         RETURN
      ENDIF
      B=-TAOCS*COS(PHIB)/TAOWS
      XB1=B+SQRT(B24ACB)
      IF (XB1 .GE. 1.0) THEN
C CRITICAL STRESS FOR BEDLOAD TRANSPORT NEVER EXCEEDED
         PERBED=0.0
         RETURN
      ENDIF

      IF (XB1 .LE. -1.0) THEN
C SECOND CASE WHERE CRITICAL STRESS FOR BEDLOAD TRANSPORT
C                                       IS ALWAYS EXCEEDED.
          TB1=PER/2.
          PERBED=100.-PERSUSP
          RETURN
      ENDIF

C CRITICAL STRESS FOR BEDLOAD TRANSPORT EXCEEDED DURING CREST
      TB1=PER/(2.*PI)*ACOS(XB1)
      XB2=B-SQRT(B24ACB)
      IF (XB2 .LE. -1.0) THEN
C CRITICAL STRESS FOR BEDLOAD TRANSPORT NOT EXCEEDED DURING TROUGH
         PERBED=200.*TB1/PER-PERSUSP
      ELSE
C CRITICAL STRESS FOR BEDLOAD TRANSPORT EXCEEDED DURING TROUGH
         TB2=PER/(2.*PI)*ACOS(XB2)
         PERBED=(2.*(TB1-TB2)+PER)/PER*100.-PERSUSP
      ENDIF

      RETURN
      END

C ****************************************************************
C SUBROUTINE PROFL
C ****************************************************************
      SUBROUTINE PROFL(D,RHOW,FALL,UB,CDIR,USTCS,USTWS,USTCWS,
     @USTCWSB,USTCWSE,USTC,USTW,USTCW,USTCRB,USTCRS,USTUP,Z0,Z0C,
     @DELTACW,C0,GAMMA0,QS,QSDIR,C0A)
 
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C THIS SUBROUTINE PREDICTS THE VELOCITY AND SUSPENDED SEDIMENT CONCENTRATION
C (SSC)PROFILES BASED ON THE GM86 COMBINED-FLOW BBL MODEL AND THE MODIFIED
C ROUSE EQUATION ACCORDING TO GLENN AND GRANT (1987).THE CALCULATED SSC AND
C VELOCITY PROFILES ARE THEN NUMERICALLY INTEGRATED TO OBTAIN THE SUSPENDED
C TRANSPORT RATE.
C 
C INPUT VARIABLES:
C
C  D = WATER DEPTH (M)
C  CDIR = CURRENT DIRECTION
C  DELTACW = HEIGHT OF THE WAVE-CURRENT BOUNDARY LAYER
C  FALL = SAND SETTLING VELOCITY
C  RHOW = FLUID DENSITY
C  UB = WAVE ORBITAL VELOCITY
C  USTC = TOTAL CURRENT SHEAR VELOCITY
C  USTCRB = CRITICAL FLUID VELOCITY FOR INITIATION OF BEDLOAD TRANSPORT (M/S)
C  USTCRS = CRITICAL FLUID VELOCITY FOR INITIATION OF SUSPENDED LOAD TRANSPORT (M/S)
C  USTCS = SKIN-FRICTION CURRENT SHEAR VELOCITY
C  USTCW = TOTAL COMBINED WAVE AND CURRENT SHEAR VELOCITY
C  USTCWS = SKIN-FRICTION COMBINED SHEAR VELOCITY                                    
C  USTCWSB = BEDLOAD COMBINED SHEAR VELOCITY
C  USTCWSE = RIPPLE-ENHANCED SKIN-FRICTION COMBINED SHEAR VELOCITY 
C  USTUP = CRITICAL FLUID VELOCITY FOR SHEET FLOW TRANSPORT (M/SEC)
C  USTW = TOTAL WAVE SHEAR VELOCITY
C  USTWS = SKIN-FRICTION WAVE SHEAR VELOCITY
C  Z0 = BED ROUGHNESS LENGTH (M)
C  Z0C = APPARENT BED ROUGHNESS LENGTH (M)
C
C OUTPUT VARIABLES:
C  C0 = REFERENCE CONCENTRATION AT Z0 (KG/M^3)
C  GAMMA0 = SAND RESUSPENSION COEFFICIENT
C  QS = SUSPENDED SEDIMENT TRANSPORT RATE (KG/M/S)
C  QSDIR = DIRECTION OF SUSPENDED SEDIMENT TRANSPORT (DEGREE)
C  C0A = DEPTH AVERAGED REFERENCE CONCENTRATION (KG/M^3)
C
C INTERMEDIATE VARIABLES:
C
C  CB = BULK BOTTOM SEDIMENT CONCENTRATION (KG/M^3)
C  CZ = PREDICTED SUSPENDED SEDIMENT CONCENTRATION AT HEIGHT Z (KG/M^3)
C  CZD = PREDICTED SUSPENDED SEDIMENT CONCENTRATION AT DELTACW (KG/M^3)
C  TAOST = NORMALIZED EXCESS SHEAR STRESS
C  UZ = PREDICTED MEAN VELOCITY AT HEIGHT Z (M/S)
C  Z = HEIGHT ABOVE THE SEABED (M)
C  ZKAPPA = VON KARMAN CONSTANT (=0.4)

C DEFINE ARRAY VARIABLES
      DIMENSION Z(0:12),CZ(12),UZ(12)
C ASSIGN HEIGHTS FOR THE PREDICTIONS OF VELOCITY AND CONCENTRATION
      DATA Z /0.0,0.01,0.02,0.03,0.05,0.07,0.1,0.2,0.3,0.5,0.7,1.0,2.0/

C GET THE UPPER LIMIT DEPTH FOR THE CONCENTRATION PROFILE  !ccf
      IL = 12
      DO I=1,IL
       IF (D.le.Z(I))THEN
        Z(I) = D
        IL = I
        GOTO 432
       END IF
      ENDDO

432   CONTINUE

C SET DEFAULT VALUES TO ZERO
      C0=0
      CZD=0
      GAMMA0=0
      QS=0
      C0A=0
      SOM=0

C ASSIGN VALUES TO CONSTANTS
      ZKAPPA=0.4
      CB=1722.5

C CONVERT SHEAR VELOCITIES TO SHEAR STRESSES
      TAOCS=RHOW*USTCS**2
      TAOWS=RHOW*USTWS**2
      TAOCWS=RHOW*USTCWS**2
      TAOCRB=RHOW*USTCRB**2

C*****************************
C COMPUTE THE VELOCITY PROFILE
C*****************************
C PURE WAVE CASE:
      IF (USTCS.EQ.0) GOTO 25 
C PURE CURRENT CASE:
      IF (UB.LT.0.01) THEN
        DO 10 I=1,IL,1
        UZ(I)=(USTC/ZKAPPA)*DLOG(Z(I)/Z0)    !al posto di ALOG 
 10     CONTINUE                             !c'e' DLOG 
      ELSE
C COMBINED WAVES AND CURRENT
        DO 20 I=1,IL,1
        IF (Z(I).LE.DELTACW) THEN
          UZ(I)=(USTC/ZKAPPA)*(USTC/USTCW)*DLOG(Z(I)/Z0)
        ELSE
          UZ(I)=(USTC/ZKAPPA)*DLOG(Z(I)/Z0C)
        ENDIF
 20     CONTINUE
      ENDIF 

C*****************************************************
C COMPUTE THE SUSPENDED SEDIMENT CONCENTRATION PROFILE
C*****************************************************
 25   CONTINUE
C PURE CURRENT CASE:
      IF (UB.LT.0.01) THEN
        IF (USTCS.LT.USTCRS) THEN
          QS=0
          GOTO 300
        ELSE
          TAOST=(TAOCS-TAOCRB)/TAOCRB
C PREDICT GAMMA0 BASED ON LI ET AL.(1996)
          IF (USTCS.GT.USTCRS.AND.USTCS.LT.USTUP) THEN
            GAMMA0=0.0206*(TAOST)**(-1.931)
          ELSE
            GAMMA0=0.00013
          ENDIF
          C0=CB*GAMMA0*TAOST
          DO 40 I=1,IL
            CZ(I)=C0*(Z(I)/Z0)**(-0.74*FALL/(0.4*USTC))
 40       CONTINUE
        ENDIF
      ELSE
        IF (USTCS.EQ.0) THEN
C PURE WAVE CASE:
          IF (USTWS.LT.USTCRS) THEN
            QS=0
            GOTO 300                          
          ELSE
            TAOST=(TAOWS-TAOCRB)/TAOCRB
C PREDICT GAMMA0 BASED ON LI ET AL.(1996)
            IF (USTWS.GT.USTCRS.AND.USTWS.LT.USTUP) THEN
              GAMMA0=0.0206*(TAOST)**(-1.931)
            ELSE
              GAMMA0=0.00013
            ENDIF
            C0=CB*GAMMA0*TAOST
            DO 60 I=1,IL
              CZ(I)=C0*(Z(I)/Z0)**(-0.74*FALL/(0.4*USTW))
 60         CONTINUE
          ENDIF
        ELSE
C COMBINED WAVE-CURRENT CASE: 
C FOR USTCWSB<USTCRS,NO SUSPENSION PROFILE WILL BE PREDICTED
          IF (USTCWSB.LT.USTCRS) THEN
            QS=0
C ASSIGN ZERO VALUES TO ALL CZ(I)
            DO 70 I=1,IL
              CZ(I)=0
 70         CONTINUE
            GOTO 300
          ELSE
C PREDICT GAMMA0 BASED ON LI ET AL.(IN 1996)
            TAOST=(TAOCWS-TAOCRB)/TAOCRB
            IF (USTCWSB.GT.USTCRS.AND.USTCWSB.LT.USTUP) THEN
              GAMMA0=0.0206*(TAOST)**(-1.931) 
            ELSE
              GAMMA0=0.00013
            ENDIF                  
C CALCULATE REFERENCE CONCENTRATIONS C0 AT Z0 AND CZD AT DELTACW
            C0=CB*GAMMA0*TAOST
            CZD=C0*(DELTACW/Z0)**(-0.74*FALL/(0.4*USTCW))
C PREDICT SUSPENSION PROFILE BASED ON THE MODIFIED ROUSE EQUATION
            DO 90 I=1,IL
              IF (Z(I).LE.DELTACW) THEN
                CZ(I)=C0*(Z(I)/Z0)**(-0.74*FALL/(0.4*USTCW))
              ELSE
                CZ(I)=CZD*(Z(I)/DELTACW)**(-0.74*FALL/(0.4*USTC))
              ENDIF
 90         CONTINUE
          ENDIF
        ENDIF
      ENDIF

C*********************************************************************
C INTEGRATE VELOCITY AND SSC PROFILE FOR SUSPENDED LOAD TRNSPORT RATE
C AND CALCULATE THE DEPTH AVERAGED EQUILIBRIUM CONCENTRATION (C0A) 	!ccf
C*********************************************************************

      Z(0)=0
      DO 95 I=1,IL
        QS=QS+(Z(I)-Z(I-1))*UZ(I)*CZ(I)
        SOM = SOM + CZ(I)
 95   CONTINUE
      C0A = SOM/IL
 
C ASSIGN THE CURRENT DIRECTION AS THE DIRECTION OF QS
      QSDIR=CDIR

 300  CONTINUE
      RETURN
      END       

C ************************************************************
C SUBROUTINE TRANSPO
C ************************************************************
      SUBROUTINE TRANSPO(D,UA,UB,U100,PHIB,PHI100,FCW,PER,GD,FRACT,
     @BETA,RHOS,RHOW,USTCRB,USTCS,USTWS,USTCWS,USTCWSB,RPLCOEF,HT,
     @CDIR,WDIR,TB1,TB2,TS1,PERBED,PERSUSP,IOPT1,VISK,DX,FALL,CONC0,
     @TAOCD,TAOCE,TIMEDR,WS,RD0,RD,RE0,RE,TIME0,QS0,SED,SEDM,SEDDIR,
     @CONC,ZB0,PHIM0,PHIM,AULVA,WSULVA,RKERO,TAOS,RES,TAOCWS,USTUP)

      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C  TRANSPO4.FOR USES EFFECTIVE COMBINED SHEAR STRESS FOR TRANSPORT
C  CALCULATION. THE TOTAL TRANSPORT DUE TO TAOCWS IS OBTAINED FIRST. THE
C  ORIGINAL COEFFICIENTS OF 0.005 AND M=0.635 ARE USED IN BAGNLOD AND YALIN
C  RESPECTIVELY. THE OPTIONS ARE:
C      CASE O: USING THE AVERAGE GM SHEAR STRESS             NO
C      CASE A: USING EFFECTIVE SHEAR STRESS TAOCWE           NO
C      CASE B: USING THE AVERAGE (TAOCR+TAOCWE)/2            NO
C      CASE C: USING 1.4*TAOCWS OF WIBERG-NELSON STRESS      NO
C      CASE D: USING NEW EFFECTIVE SHEAR STRESS              YES
C      CASE E: USING 1.65*TCWS OF WIBERG-NELSON              NO
C
C  THIS SUBROUTINE CALCULATES THE TIME-AVERAGED NET SEDIMENT TRANSPORT
C  BY A CHOICE OF METHODS.  FOR THE PURE WAVE CASE THERE IS NO NET
C  TRANSPORT SINCE TRANSPORT DURING THE WAVE CREST IS EQUAL AND OPPOSITE
C  TO THAT DURING THE WAVE TROUGH (DUE TO THE USE OF LINEAR WAVE THEORY).
C  FOR THE PURE CURRENT AND MIXED CONDITIONS, THE USER MAKES A CHOICE
C  BETWEEN TRANSPORT FORMULAE, HOWEVER IF SUSPENDED LOAD TRANSPORT IS
C  SIGNIFICANT IT IS RECOMMENDED THAT A TOTAL LOAD FORMULA BE USED.
C
C  THE OPTIONS AVAILABLE ARE:
C
C            1 - ENGELUND-HANSEN (1967) TOTAL LOAD EQUATION,
C            2 - EINSTEIN-BROWN (1950) BEDLOAD EQUATION,
C            3 - BAGNOLD (1963) TOTAL LOAD EQUATION,
C            4 - YALIN (1963) BEDLOAD EQUATION,
C            5 - VAN RIJN (1993) BEDLOAD EQUATION,
C            7 - AMOS AND GREENBERG (1980) COHESIVE SEDIMENTS.
C
C   THE CHOICE IS CONTROLLED BE THE VALUE OF "IOPT1" (1 TO 7) WHICH
C   IS READ IN IN SUBROUTINE READIN (READBCH FOR THE BATCH PROGRAM)
C
C INPUT VARIABLES:
C       D = WATER DEPTH (M)                                          (INDATA96 file)
C     PER = WAVE PERIOD (SEC)                                        (INDATA96 file)
C      GD = SEDIMENT GRAIN SIZE (M)                                  (INDATA96 file)
C   FRACT = FRACTION OF THE TOTAL SEDIMENT WITH GRAIN SIZE GD        (INDATA96 file)
C    BETA = BED SLOPE (DEGREE)                                       (INDATA96 file)
C    CDIR = CURRENT DIRECTION (AZIMUTH,DEGREES)                      (INDATA96 file)
C    WDIR = WAVE DIRECTION (AZIMUTH,DEGREES)                         (INDATA96 file)
C   IOPT1 = OPTION SELECTED FOR SEDIMENT TRANSPORT FORMULA           (INDATA96 file)
C   CONC0 = INITIAL ESTIMATE OF SEDIMENT CONCENTRATION (mg/l)     *  (INDATA96 file)
C   TAOCD = CRITICAL STRESS FOR DEPOSITION (Pa)                   *  (INDATA96 file)
C   TAOCE = CRITICAL STRESS FOR EROSION (Pa)                      *  (INDATA96 file)
C  TIMEDR = DEPOSITION OR EROSION DURATION (minutes)              *  (INDATA96 file)
C      WS = SETTLING VELOCITY (m/s)                               *  (INDATA96 file)
C     ZB0 = DEPTH OF LAYER EXPOSED TO THE FLOW (m)                *  (INDATA96 file)
C   AULVA = PERC. OF BED AREA COVERED BY THE ALGAE "ULVA" (%)     *  (INDATA96 file)
C  WSULVA = SETTLING VELOCITY OF ULVA (m/s)                       *  (INDATA96 file)
C   RKERO = PROPORT. COEFFICIENT FOR EROSION RATE (DEFAULT = 2.0) *  (INDATA96 file)
C    RHOS = SEDIMENT DENSITY (KG/M**3)                               (READIN subroutine)
C    RHOW = FLUID DENSITY (KG/M**3)                                  (READIN subroutine)
C      UB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE VELOCITY (M/SEC)    (OSCIL subroutine)
C  USTCRB = CRITICAL SHEAR VELOCITY FOR BEDLOAD TRANSPORT            (THRESH subroutine)
C      UA = CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC (M/SEC)   (FRICFAC subroutine)
C    U100 = CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)               (FRICFAC subroutine)
C    PHIB = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS WITHIN THE     (FRICFAC subroutine)
C           WAVE BOUNDARY LAYER (RADIANS)
C  PHI100 = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M.        (FRICFAC subroutine)
C           ABOVE SEABED (RADIANS)
C     FCW = BOTTOM FRICTION FACTOR                                   (FRICFAC subroutine)
C RPLCOEF = RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION         (FRICFAC subroutine)
C   USTCS = MAXIMUM CURRENT SKIN-FRICTION SHEAR VELOCITY             (FRICFAC subroutine)
C   USTWS = GM WAVE SKIN-FRICTION SHEAR VELOCITY                     (FRICFAC subroutine)
C  USTCWS = GM COMBINED WAVE-CURRENT SKIN-FRICTION SHEAR VELOCITY    (FRICFAC subroutine)
C USTCWSB = TRANSPORT-RELATED COMBINED SHEAR VELOCITY                (FRICFAC subroutine)
C     TB1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD      (TIMING subroutine)
C           TRANSPORT CEASES (SEC)
C     TB2 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD      (TIMING subroutine)
C           TRANSPORT RECOMMENCES (SEC)
C     TS1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH SUSPENDED    (TIMING subroutine)
C           LOAD TRANSPORT CEASES (SEC)
C  PERBED = PERCENTAGE OF TIME SPENT IN BEDLOAD TRANSPORT PHASE      (TIMING subroutine)
C PERSUSP = PERCENTAGE OF TIME SPENT IN SUSP. LOAD TRANSPORT PHASE   (TIMING subroutine)
C    VISK = KINEMATIC VISCOSITY OF FLUID (M**2/SEC)
C      DX = DIMENSIONLESS GRAIN SIZE
C    FALL = SPHERICAL-GRAIN SETTLING VELOCITY [M/S]
C      HT = WAVE HEIGHT (M)
C
C OUTPUT VARIABLES:
C     SED = TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME TRANSPORTED PER
C           UNIT BED WIDTH PER UNIT TIME (M**3/M/S)
C    SEDM = TIME-AVERAGED NET SEDIMENT TRANSPORT AS MASS OF SEDIMENT
C           SOLIDS TRANSPORTED PER UNIT BED WIDTH PER UNIT TIME (KG/M/S)
C  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)
C
C INPUT/OUTPUT VARIABLES USED FOR COHESIVE SEDIMENT TRANSPORT CALCULATIONS
C    CONC = FINAL CALCULATED SEDIMENT CONCENTRATION (mg/l)
C     RE0 = INITIAL EROSION RATE (kg/m^2/s)
C      RE = EROSION RATE (kg/m^2/s)
C     RD0 = INITIAL DEPOSITION RATE (kg/m^2/s)
C      RD = DEPOSITION RATE (kg/m^2/s)
C   TIME0 = CALCULATED TIME (minutes) WHEN CONC. HAS DECREASED TO BE LESS
C           LESS THAN 1 MG/L DUE TO DEPOSITION OR WHEN DOWN-CORE CRITICAL
C           EROSION SHEAR STRESS HAS BECOME EQUAL TO THE BOTTOM SHEAR
C           STRESS SO THAT EROSION WILL STOP
C     QS0 = INITIAL MASS SEDIMENT TRANSPORT RATE (kg/m/s)
C
C INTERMEDIATE VARIABLES:
C      G = GRAVITY ACCELERATION
C   DRHO = RHOS-RHOW
C DGAMMA = G*DRHO
C    PHI = ANGLE OF REPOSE : ESTIMATED TO BE 30
C      S = SPECIFIC GRAVITY OF WATER
C    VCB = CRITICAL FLUID VELOCITY FOR INITIATION OF BEDLOAD TRANSPORT
C     DS = DIFFERENCE BETWEEN SPECIFIC GRAVITY OF SEDIMENT AND WATER
C  TAOCS = SKINFRICTION CURRENT SHEAR STRESS
C  TAOWS = SKINFRICTION WAVE SHEAR STRESS

C----------------------------------------------
C INITIALIZE AND DEFINE CONSTANTS AND VARIABLES
C----------------------------------------------
      G=9.81
      PI=2.*ASIN(1.)
      DRHO=RHOS-RHOW
      DGAMMA=G*DRHO
      PHI=30
      IF (PER .NE. 0.0) W=2.*PI/PER
      VCB=0.0                        !ggu INTEL
      SED=0.0
      SEDM=0.0
      SEDDIR=0.0
      RE0=0.0
      RE=0.0
      RD0=0.0
      RD=0.0
      QS0=0.0
      RLWRT = 0.0
      TAO0 = 0.0

C COMPUTE RIPPLE BREAKOFF
      S=RHOS/RHOW
      SST=GD*((S-1)*G*GD)**0.5/(4*VISK)
      PSICR=(USTCRB**2)/((S-1)*G*GD)
      PSIBF=1.8*(SST**0.6)*PSICR
      USTBF=(PSIBF*(S-1)*G*GD)**0.5

C CALCULATE BEDLOAD THRESHOLD FLOW VELOCITY VCB
      TAOCRB=RHOW*USTCRB**2
      VCB=SQRT(2.*TAOCRB/(RHOW*FCW))

C******************************
C  WAVES ONLY (NO NET CURRENT)
C******************************
C  FOR THE PURE WAVE CASE, NO NET TRANSPORT OCCURS. EXIT PROGRAM.
C      IF (UA .EQ. 0.0 .AND. IOPT1. NE. 7) RETURN

C******************************
C  CURRENT ONLY (NO WAVES)
C******************************
C  NO INTEGRATION IS REQUIRED FOR THE PURE CURRENT CASE.
C  WHEN TRANSPORT IS ALL IN SUSPENDED LOAD, THEN A WARNING MESSAGE
C  IS PRINTED CONCERNING THE USE OF "BEDLOAD" TRANSPORT PREDICTORS

C  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 888

C CHECK IF THERE IS ANY TRANSPORT FOR NON-COHESIVE SEDIMENTS,
C IF NOT THEN EXIT.
      IF (IOPT1.NE.7) THEN
         IF (PERBED .EQ. 0.0 .AND. PERSUSP .EQ. 0.0) RETURN
      ENDIF

C OBTAIN CURRENT SHEAR STRESS TAO0, UA=U100 FOR STEADY CURRENT
C      TAO0=RHOW*FCW/2.*UA**2
      TAO0=RHOW*USTCS**2
      
      GOTO 777

 888  CONTINUE

C******************************
C  COMBINED WAVE AND CURRENT 
C******************************
C
C  THE COMBINED WAVE AND CURRENT CASE REQUIRES INTEGRATION OF THE
C  INSTANTANEOUS TRANSPORT OVER THE WAVE PERIOD. THE USE OF LWT ALLOWS
C  INTEGRATION TO BE DONE OVER ONLY HALF A WAVE CYCLE. BAGNOLD'S METHOD
C  DOES NOT REQUIRE INTEGRATION. THE X- AND Y- COMPONENTS OF TRANSPORT 
C  ARE CONSIDERED SEPARATELY, WHERE THE X-COMPONENT IS PARALLEL TO THE 
C  WAVE DIRECTION AND THE Y-COMPONENT IS NORMAL TO THE WAVE DIRECTION.
C
C  INTERMEDIATE VARIABLES:
C      STEPC = INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER CREST
C      STEPT = INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER TROUGH 
C      TIMEC = SEDIMENT TRANSPORT TIME UNDER CREST
C      TIMET = SEDIMENT TRANSPORT TIME UNDER TROUGH 
C      RLWRT = LOWER LIMIT OF TRANSPORT INTEGRATION UNDER TROUGH

C  FOR IOPT1 TO IOPT5, CHECK IF CRITICAL STRESS IS EVER EXCEEDED, IF NOT
C  THEN EXIT.
      IF (IOPT1.NE.7) THEN   
         IF (TB1 .EQ. 0.0 .AND. TS1 .EQ. 0.0) RETURN
         IF (PERBED .EQ. 0.0 .AND. PERSUSP .EQ. 0.0) RETURN
      ENDIF

C COMPUTE THE TRANSPORT TIMES FOR WAVE CREST AND TROUGH RESPECTIVELY
      IF (TB1 .EQ. PER/2. .OR. TS1 .EQ. PER/2.) THEN
         TIMEC = PER/4.
         TIMET = PER/4.
         RLWRT = PER/4.
      ELSE IF (TB1 .GT. 0. .AND. TB2 .GT. 0.) THEN
         TIMEC = TB1
         TIMET = PER/2. - TB2
         RLWRT = TB2
      ELSE 
         TIMEC=TB1                 
         TIMET=0.
      ENDIF

C COMPUTE THE INTEGRATION TIME STEPS
      IF (TIMEC .GE. 10.) THEN
         NC = 20.
      ELSE
         NC = 10.
      ENDIF
      IF (TIMET .GE. 10.) THEN
         NT = 20.
      ELSE
         NT = 10.
      ENDIF
      STEPC = TIMEC/NC
      STEPT = TIMET/NT

 777  CONTINUE

C-----------------------------------
C DECIDE FORMULA ACCORDING TO IOPT1
C-----------------------------------
      
      GOTO (1,2,3,4,5,6,7) IOPT1

 1    CALL ENGHAN(RHOW,DGAMMA,GD,U100,USTCS,USTWS,USTCWS,
     @USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,TIMET,
     @STEPC,STEPT,RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,SED,SEDDIR)
      GOTO 998

 2    CALL EINBWN(RHOW,DGAMMA,GD,FALL,USTCS,USTWS,USTCWS,
     @USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,TIMET,
     @STEPC,STEPT,RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,SED,SEDDIR)
      GOTO 998
 
 3    CALL BAGNLD(RHOW,DGAMMA,U100,CDIR,USTCWS,USTCWSB,
     @USTCRB,USTBF,USTUP,RPLCOEF,SED,SEDDIR,UB,GD,RHOS,VCB)
      GOTO 998

 4    CALL YALIN(RHOW,RHOS,DGAMMA,GD,USTCS,USTWS,USTCWS,
     @USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,TIMET,
     @STEPC,STEPT,RLWRT,UB,FCW,UA,VCB,TAOCRB,G,DRHO,IOPT1,
     @PER,PHI100,CDIR,WDIR,SED,SEDDIR)
      GOTO 998

 5    CALL VANRIJN(RHOW,RHOS,HT,D,DX,GD,USTCS,USTWS,USTCWS,
     @USTCWSB,TAOCRB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,
     @TIMET,STEPC,STEPT,RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,
     @SED,SEDDIR)
      GOTO 998

 6    CONTINUE

 7    CALL AMOSGB(RHOW,RHOS,CONC0,TAOCE,TAOCD,USTCWS,WS,TIMEDR,
     @U100,D,RD0,RD,RE0,RE,TIME0,QS0,SED,CONC,ZB0,PHIM0,PHIM,TAOCWS,
     @TAOS,RES,AULVA,WSULVA,RKERO,UB,USTCS,UA,TAO0)
      SEDDIR=CDIR

 998  CONTINUE

C INLUDE BED-SLOPE EFFECT AND THEN CONVERT SED FROM VOLUME FLUX (M^2/M/S)
C TO MASS FLUX SEDM IN KG/SEC/M.

      SED=SED*(1-TAN(BETA/57.3)/TAN(PHI/57.3))
      SEDM = RHOS*SED
      RETURN
      END

C****************************************************************************
C***********************************************************************

      SUBROUTINE ENGHAN(RHOW,DGAMMA,GD,U100,USTCS,USTWS,USTCWS,
     @USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,TIMET,
     @STEPC,STEPT,RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,SED,SEDDIR)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C THIS SUBROUTINE CALCULATES SEDIMENT TRANSPORT RATES ACCORDING TO
C THE ENGELUND AND HANSEN (1967) TOTAL LOAD EQUATION.
C
C OUTPUT VARIABLE:
C     SED = VOLUME RATE OF SEDIMENT TRANSPORT PER UNIT WIDTH OF BED
C  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

C  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 123

C****************************
C  CURRENT ONLY (NO WAVES)
C****************************

      V=U100
      SED=0.05*V**2*SQRT(TAO0**3*RHOW)/(GD*DGAMMA**2)
      SEDDIR=CDIR
      RETURN

 123  CONTINUE

C*****************************
C  COMBINED WAVE AND CURRENT
C*****************************

      CONST1=0.05*(RHOW*U100/DGAMMA)**2/GD
      CONST2=3.

      CALL WAVEINT(IOPT1,CONST1,CONST2,RHOW,USTCS,USTWS,USTCWS,
     @USTCWSB,TAOCRB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,
     @TIMET,STEPC,STEPT,RLWRT,PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      RETURN
      END

C*********************************************************************
C***********************************************************************

      SUBROUTINE EINBWN(RHOW,DGAMMA,GD,FALL,USTCS,USTWS,USTCWS,
     @USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,TIMET,
     @STEPC,STEPT,RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,SED,SEDDIR)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C THIS SUBROUTINE CALCULATES SEDIMENT TRANSPORT RATES ACCORDING TO
C THE EINSTEIN AND BROWN (1950) BEDLOAD EQUATION.
C
C OUTPUT VARIABLE:
C     SED = VOLUME RATE OF SEDIMENT TRANSPORT PER UNIT WIDTH OF BED
C  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

C  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 123

C****************************
C  CURRENT ONLY (NO WAVES)
C****************************

      SED = 40.0*FALL*GD*(TAO0/(DGAMMA*GD))**3
      SEDDIR=CDIR
      RETURN

 123  CONTINUE

C*****************************
C  COMBINED WAVE AND CURRENT
C*****************************

      CONST1=40*FALL*GD*(RHOW/(GD*DGAMMA))**3
      CONST2=6.

      CALL WAVEINT(IOPT1,CONST1,CONST2,RHOW,USTCS,USTWS,USTCWS,
     @USTCWSB,TAOCRB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,
     @TIMET,STEPC,STEPT,RLWRT,PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      RETURN
      END

C*********************************************************************
C*********************************************************************

      SUBROUTINE BAGNLD(RHOW,DGAMMA,U100,CDIR,USTCWS,USTCWSB,
     @USTCRB,USTBF,USTUP,RPLCOEF,SED,SEDDIR,UB,GD,RHOS,VCB)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C THIS SUBROUTINE CALCULATES SEDIMENT TRANSPORT RATES ACCORDING TO
C THE BAGNOLD (1963) TOTAL LOAD EQUATION.
C
C OUTPUT VARIABLE:
C     SED = VOLUME RATE OF SEDIMENT TRANSPORT PER UNIT WIDTH OF BED
C  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

C  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 123

C****************************
C  CURRENT ONLY (NO WAVES)
C****************************

      BETA=1.73D0
      IF (GD .LE. 0.00031) BETA=7.22D0
      SED=BETA/RHOS*(U100-VCB)**3
      SEDDIR=CDIR
      RETURN

 123  CONTINUE

C*****************************
C  COMBINED WAVE AND CURRENT
C*****************************

      USTCWSGM=USTCWS
      ALPHA=(USTUP-USTCWSB)/(USTUP-USTBF)
C COMPUTE EFFECTIVE SHEAR VELOCITY AT THE RIPPLE CREST
      USTCWSE=USTCWS*RPLCOEF
C CONVERT SHEAR VELOCITIES TO SHEAR STRESSES
      TAOCRB = RHOW*USTCRB**2
      TAOCWS = RHOW*USTCWS**2
      TAOCWSE = RHOW*USTCWSE**2

C COMPUTE THE EFFECTIVE SHEAR STRESS CAUSING SEDIMENT TRANSPORT
C -----------------------------------------
C CASE O: NO
C USING THE AVERAGE GM SHEAR STRESS
C      TAOCWS=TAOCWS
C -----------------------------------------
C -----------------------------------------
C CASE A: NO
C USING THE RIPPLE-ENHANCED SHEAR STRESS
C      TAOCWS=TAOCWSE
C -----------------------------------------
C -----------------------------------------
C CASE B: NO
C USING THE AVERAGE SHEAR STRESS OF TAOCRB AND TAOCWSE
C      TAOCWS = 0.5*(TAOCRB+TAOCWSE)
C -----------------------------------------
C -------------------------------------------
C CASE D: YES
C USING THE NEW EFFECTIVE SHEAR STRESS

C FOR WEAK-TRANSPORT RIPPLES
      IF (USTCWSGM .LT. USTCRB) THEN
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
      ENDIF

C FOR EQUILIBRIUM RIPPLES
      IF (USTCWSB .GE. USTCRB .AND. USTCWSB .LT. USTBF) THEN
C CHOICE A: USE (TAOCRB+TAOCWS+TAOCWSE)/3 FOR USTCWSB<USTBF
C         TAOCWS=0.333*(TAOCRB+TAOCWS+TAOCWSE)
C CHOICE B: USE (TAOCRB+TAOCWSE)/2 FOR USTCWSB<USTBF
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
      ENDIF

C FOR BREAK-OFF RIPPLES
      IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
         TAOCWS=(1/(2+ALPHA))*(ALPHA*TAOCRB+TAOCWS+TAOCWSE)
      ENDIF

C FOR UPPER-PLANE BED
      IF (USTCWSB .GE. USTUP) TAOCWS=TAOCWS
C -------------------------------------------

C COMPUTE NORMALIZED EXCESS SHEAR STRESS EST
      EST = (TAOCWS - TAOCRB)/TAOCRB

C CALCULATE TOTAL TRANSPORT RATE; RK IS BASED ON STERNBERG (1972)
        IF (EST .LT. 0) THEN
           SED = 0
        ELSE
           RK = 0.005*EXP(0.7*EST)
           SED=RK*TAOCWS*U100/DGAMMA
        ENDIF
        SEDDIR=CDIR
      RETURN
      END

C*********************************************************************
C*********************************************************************

      SUBROUTINE YALIN(RHOW,RHOS,DGAMMA,GD,USTCS,USTWS,USTCWS,
     @USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,TIMET,
     @STEPC,STEPT,RLWRT,UB,FCW,UA,VCB,TAOCRB,G,DRHO,IOPT1,
     @PER,PHI100,CDIR,WDIR,SED,SEDDIR)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C THIS SUBROUTINE CALCULATES SEDIMENT TRANSPORT RATES ACCORDING TO
C THE YALIN (1963) BEDLOAD EQUATION.
C
C OUTPUT VARIABLE:
C     SED = VOLUME RATE OF SEDIMENT TRANSPORT PER UNIT WIDTH OF BED
C  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

C  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 123

C****************************
C  CURRENT ONLY (NO WAVES)
C****************************

      USTAR=SQRT(FCW/2.)*UA
      S=(UA/VCB)**2-1.0
      A=2.45*(RHOW/RHOS)**0.4*SQRT(TAOCRB/(G*DRHO*GD))
      SED=0.635*GD*USTAR*S*(1.0-DLOG(1.0+A*S)/(A*S))
      SEDDIR=CDIR
      RETURN

 123  CONTINUE

C*****************************
C  COMBINED WAVE AND CURRENT
C*****************************

      CONST1=0.635*GD
      CONST2=2.45*(RHOW/RHOS)**0.4*SQRT(TAOCRB/(G*DRHO*GD))

      CALL WAVEINT(IOPT1,CONST1,CONST2,RHOW,USTCS,USTWS,USTCWS,
     @USTCWSB,TAOCRB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,
     @TIMET,STEPC,STEPT,RLWRT,PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      RETURN
      END

C*********************************************************************
C*********************************************************************

      SUBROUTINE VANRIJN(RHOW,RHOS,HT,D,DX,GD,USTCS,USTWS,USTCWS,
     @USTCWSB,TAOCRB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,
     @TIMET,STEPC,STEPT,RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,
     @SED,SEDDIR)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C THIS SUBROUTINE CALCULATES SEDIMENT TRANSPORT RATES ACCORDING TO
C THE VAN RIJN (1993) BEDLOAD EQUATION
C
C OUTPUT VARIABLE:
C     SED = VOLUME RATE OF SEDIMENT TRANSPORT PER UNIT WIDTH OF BED
C  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)
C
C INTERMEDIATE VARIABLE:
C     S = INSTANTANEOUS SHEAR STRESS PARAMETER [ADIMENSIONAL]
C     A = CALIBRATION FACTOR

      G=9.81

C  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 123

C****************************
C  CURRENT ONLY (NO WAVES)
C****************************

      F = RHOS/RHOW
      F1 = F - 1.
      S = TAO0/TAOCRB - 1.

      SED = 0.053*(F1*G)**0.5 * GD**1.5 * DX**(-0.3) * S**2.1
      SEDDIR=CDIR
      RETURN

 123  CONTINUE

C*****************************
C  COMBINED WAVE AND CURRENT
C*****************************

      A = 1. - (HT/D)**0.5
      CONST1=0.25*A*GD*DX**(-0.3)
      CONST2=1.

      CALL WAVEINT(IOPT1,CONST1,CONST2,RHOW,USTCS,USTWS,USTCWS,
     @USTCWSB,TAOCRB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,
     @TIMET,STEPC,STEPT,RLWRT,PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      RETURN
      END

C*********************************************************************

      SUBROUTINE WAVEINT(IOPT1,CONST1,CONST2,RHOW,USTCS,USTWS,USTCWS,
     @USTCWSB,TAOCRB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMEC,
     @TIMET,STEPC,STEPT,RLWRT,PER,PHI100,CDIR,WDIR,SED,SEDDIR)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C THIS SUBROUTINE COMPUTE THE TIME AVERAGED TRANSPORT (AS VOLUME)
C THROUGH THE INTEGRATION OVER A WAVE CYCLE
C
C  INTERMEDIATE VARIABLES:
C         S = INSTANTANEOUS SHEAR STRESS PARAMETER [ADIMENSIONAL]
C       GSXC = VOLUME TRANSPORT IN WAVE DIRECTION IN TIME STEPC
C       GSXT = VOLUME TRANSPORT IN WAVE DIRECTION IN TIME STEPT
C      SEDXC = TOTAL VOLUME TRANSPORT IN WAVE DIRECTION UNDER CREST   
C      SEDXT = TOTAL VOLUME TRANSPORT IN WAVE DIRECTION UNDER TROUGH   
C      SEDYC = TOTAL VOLUME TRANSPORT IN Y DIRECTION UNDER CREST   
C      SEDYT = TOTAL VOLUME TRANSPORT IN Y DIRECTION UNDER TROUGH   

      PI=2.*ASIN(1.)
      GSXC = 0.0
      GSXT = 0.0
      GSYC = 0.0
      GSYT = 0.0
      SEDXC= 0.0
      SEDYC= 0.0
      SEDXT= 0.0
      SEDYT= 0.0
      CONST3 = CONST2

C PRESERVE THE BURST-AVERAGED SHEAR VELOCITIES
      USTCSGM=USTCS
      USTWSGM=USTWS
      USTCWSGM=USTCWS
      ALPHA=(USTUP-USTCWSB)/(USTUP-USTBF)

C CONVERT SHEAR VELOCITIES TO SHEAR STRESSES
      TAOCRB=RHOW*USTCRB**2
      TAOCS=RHOW*USTCS**2
      TAOWS=RHOW*USTWS**2

C COMPUTE STEADY CURRENT SHEAR STRESS IN THE X DIRECTION
      TAOCSX=TAOCS*COS(PHIB)

C COMPUTE STEADY CURRENT SHEAR STRESS IN THE Y DIRECTION
      TAOCSY=TAOCS*SIN(PHIB)

C ===================================================================
C COMPUTE TRANSPORT VOLUME UNDER THE WAVE CREST
      DO 111 I = 0, (NC-1)
         TAOCWSX=TAOCSX+0.5*TAOWS*(COS(W*I*STEPC)+COS(W*(I+1)*STEPC))
         TAOCWS=SQRT(TAOCSY**2+TAOCWSX**2)
         TAOCWSE=(RPLCOEF**2)*TAOCWS

C -----------------------------------------
C CASE O: NO
C USING THE AVERAGE GM SHEAR STRESS
C         USTCWS=SQRT(TAOCWS/RHOW)
C -----------------------------------------
C -------------------------------------------
C CASE A: NO
C USING EFFECTIVE SHEAR STRESS
C         USTCWS=RPLCOEF*SQRT(TAOCWS/RHOW)
C -------------------------------------------
C -------------------------------------------
C CASE B: NO
C USING THE AVERAGED EFFECTIVE SHEAR STRESS OF TAOCRB AND TAOCWSE
C         TAOCWS=0.5*(TAOCRB+TAOCWSE)
C         USTCWS=SQRT(TAOCWS/RHOW)
C -----------------------------------------
C -----------------------------------------
C CASE C: NO OR CASE E: NO
C USING THE EFFECTIVE SHEAR STRESS OF WIBERG-NELSON (1992)
C
C FOR CASE C
C         IF (USTCWSB .LT. USTBF) TAOCWS=1.4*TAOCWS
C         IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
C            TAOCWS=(1+0.4*ALPHA)*TAOCWS
C         ENDIF
C         IF (USTCWSB .GE. USTUP) TAOCWS=TAOCWS
C
C FOR CASE E
C         IF (USTCWSB .LT. USTBF) TAOCWS=1.65*TAOCWS
C         IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
C            TAOCWS=(1+0.65*ALPHA)*TAOCWS
C         ENDIF
C         IF (USTCWSB .GE. USTUP) TAOCWS=TAOCWS
C
C         USTCWS=SQRT(TAOCWS/RHOW)
C -------------------------------------------
C -------------------------------------------
C CASE D: YES
C USING THE NEW EFFECTIVE SHEAR STRESS

C FOR WEAK-TRANSPORT RIPPLES
      IF (USTCWSGM .LT. USTCRB) THEN
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

C FOR EQUILIBRIUM RIPPLES
      IF (USTCWSB .GE. USTCRB .AND. USTCWSB .LT. USTBF) THEN
C CHOICE A: USE (TAOCRB+TAOCWS+TAOCWSE)/3 FOR USTCWSB<USTBF
C         TAOCWS=0.333*(TAOCRB+TAOCWS+TAOCWSE)
C         USTCWS=SQRT(TAOCWS/RHOW)
C CHOICE B: USE (TAOCRB+TAOCWSE)/2 FOR USTCWSB<USTBF
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

C FOR BREAK-OFF RIPPLES
      IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
         TAOCWS=(1/(2+ALPHA))*(ALPHA*TAOCRB+TAOCWS+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

C FOR UPPER-PLANE BED
      IF (USTCWSB .GE. USTUP) USTCWS=SQRT(TAOCWS/RHOW)
C -------------------------------------------

         S=TAOCWS/TAOCRB - 1.
         IF (S .LT. 0) S = 0.0
         IF(IOPT1.EQ.4)THEN
           FACTOR = DABS(S-LOG(1+CONST3*S)/CONST3)
           CONST2 = 1.
         ELSEIF(IOPT1.EQ.5)THEN
           FACTOR = S**1.5
         ELSE
           FACTOR = 1.
         ENDIF

         SED=CONST1*STEPC*FACTOR*USTCWS**CONST2
         PHIS=ATAN2(TAOCSY,TAOCWSX)
         GSXC=SED*COS(PHIS)
         GSYC=SED*SIN(PHIS)
         SEDXC=SEDXC+GSXC
         SEDYC=SEDYC+GSYC
 111  CONTINUE
      SEDXC=2*SEDXC
      SEDYC=2*SEDYC

C ===================================================================
C COMPUTE TRANSPORT VOLUME UNDER THE WAVE TROUGH

C IF NO TRANSPORT OCCURRED UNDER WAVE TROUGH, EXIT
      IF (TIMET .EQ. 0.) GO TO 333

      DO 222 I = 0, (NT-1)
         TAOCWSX=TAOCSX+0.5*TAOWS*(COS(W*(RLWRT+I*STEPT))
     @           +COS(W*(RLWRT+(I+1)*STEPT)))
         TAOCWS=SQRT(TAOCSY**2+TAOCWSX**2)
         TAOCWSE=(RPLCOEF**2)*TAOCWS

C -------------------------------------------
C CASE O: NO
C USING THE AVERAGE GM SHEAR STRESS
C         USTCWS=SQRT(TAOCWS/RHOW)
C -------------------------------------------
C -------------------------------------------
C CASE A: NO
C USING EFFECTIVE SHEAR STRESS
C         USTCWS=RPLCOEF*SQRT(TAOCWS/RHOW)
C -------------------------------------------
C -----------------------------------------
C CASE B: NO
C USING THE AVERAGED EFFECTIVE SHEAR STRESS 0.5(TAOCRB+TAOCWSE)
C         TAOCWS=0.5*(TAOCRB+TAOCWSE)
C         USTCWS=SQRT(TAOCWS/RHOW)
C -----------------------------------------
C -----------------------------------------
C CASE C: NO OR CASE E: NO
C USING THE EFFECTIVE SHEAR STRESS OF WIBERG-NELSON (1992)
C
C FOR CASE C, TAOCWSE=1.4TAOCWS
C         IF (USTCWSB .LT. USTBF) TAOCWS=1.4*TAOCWS
C         IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
C            TAOCWS=(1+0.4*ALPHA)*TAOCWS
C         ENDIF
C         IF (USTCWSB .GE. USTUP) TAOCWS=TAOCWS
C
C FOR CASE E, TAOCWSE=1.65TAOCWS
C         IF (USTCWSB .LT. USTBF) TAOCWS=1.65*TAOCWS
C         IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
C            TAOCWS=(1+0.65*ALPHA)*TAOCWS
C         ENDIF
C         IF (USTCWSB .GE. USTUP) TAOCWS=TAOCWS
C
C         USTCWS=SQRT(TAOCWS/RHOW)
C -------------------------------------------
C -------------------------------------------
C CASE D: YES
C USING THE NEW EFFECTIVE SHEAR STRESS

C FOR WEAK-TRANSPORT RIPPLES
      IF (USTCWSGM .LT. USTCRB) THEN
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

C FOR EQUILIBRIUM RIPPLES
      IF (USTCWSB .GE. USTCRB .AND. USTCWSB .LT. USTBF) THEN
C CHOICE A: USE (TAOCRB+TAOCWS+TAOCWSE)/3 FOR USTCWSB<USTBF
C         TAOCWS=0.333*(TAOCRB+TAOCWS+TAOCWSE)
C         USTCWS=SQRT(TAOCWS/RHOW)
C CHOICE B: USE (TAOCRB+TAOCWSE)/2 FOR USTCWSB<USTBF
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

C FOR BREAK-OFF RIPPLES
      IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
         TAOCWS=(1/(2+ALPHA))*(ALPHA*TAOCRB+TAOCWS+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

C FOR UPPER-PLANE BED
      IF (USTCWSB .GE. USTUP) USTCWS=SQRT(TAOCWS/RHOW)
C -------------------------------------------

         S=TAOCWS/TAOCRB - 1.
         IF (S .LT. 0) S = 0.0
         IF(IOPT1.EQ.4)THEN
           FACTOR = DABS(S-LOG(1+CONST3*S)/CONST3)
           CONST2 = 1.
         ELSEIF(IOPT1.EQ.5)THEN
           FACTOR = S**1.5
         ELSE
           FACTOR = 1.
         ENDIF

         SED=CONST1*STEPT*FACTOR*USTCWS**CONST2
         PHIS=ATAN2(TAOCSY,TAOCWSX)
         IF (PHIS .LT. 0) THEN
               GSXT=-SED*COS(PHIS)
               GSYT=-SED*SIN(PHIS)
         ELSE
               GSXT=SED*COS(PHIS)
               GSYT=SED*SIN(PHIS)
         ENDIF
         SEDXT=SEDXT+GSXT
         SEDYT=SEDYT+GSYT
 222  CONTINUE
      SEDXT=2*SEDXT
      SEDYT=2*SEDYT
C ===================================================================

 333  CONTINUE

C RETURN THE BURST-AVERAGED SHEAR VELOCITIES
      USTCS=USTCSGM
      USTWS=USTWSGM
      USTCWS=USTCWSGM

C COMPUTE THE DIRECTION OF THE TRANSPORT
      SEDX=(SEDXC+SEDXT)/PER
      SEDY=(SEDYC+SEDYT)/PER
      SED=SQRT(SEDX**2+SEDY**2)
      IF (SEDY .EQ. 0. .AND. SEDX .EQ. 0.) THEN
         SEDDIR = 0.
      ELSE
         PHIS=ATAN2(SEDY,SEDX)
         DIF=SIGN((PHI100-PHIS)*180./PI,CDIR-WDIR)
         CWDIF=ABS(CDIR-WDIR)
         IF(CWDIF .LE. 90.0) SEDDIR=CDIR-DIF
         IF (CWDIF .LE. 180.0 .AND. CWDIF .GT. 90.0) SEDDIR=CDIR+DIF
         IF (CWDIF .LE. 270.0 .AND. CWDIF .GT. 180.0) SEDDIR=CDIR-DIF
         IF (CWDIF .LE. 360.0 .AND. CWDIF .GT. 270.0) SEDDIR=CDIR+DIF
         IF (SEDDIR .LT. 0.0) SEDDIR=SEDDIR+360.0
         IF (SEDDIR .GE. 360.0) SEDDIR=SEDDIR-360.0
      ENDIF

      IF(USTCS .EQ. 0.) SEDDIR = WDIR

      RETURN
      END

C*********************************************************************
C*********************************************************************

      SUBROUTINE AMOSGB(RHOW,RHOS,CONC0,TAOCE,TAOCD,USTCWS,WS,TIMEDR,
     @U100,D,RD0,RD,RE0,RE,TIME0,QS0,SED,CONC,ZB0,PHIM0,PHIM,TAOCWS,
     @TAOS,RES,AULVA,WSULVA,RKERO,UB,USTCS,UA,TAO0)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)

C THIS SUBROUTINE CALCULATES COHESIVE SEDIMENT TRANSPORT RATES FOR
C PURE CURRENTS ACCORDING TO THE IMPROVED AMOS and GREENBERG METHOD (AMOS
C AND GREENBURG, 1980; AMOS ET AL., 1992; AMOS ET AL, IN REVIEW)
C
C OUTPUT VARIABLES:
C   RD0 = INITIAL DEPOSITION RATE
C    RD = DEPOSITION RATE
C   RE0 = INITIAL EROSION RATE
C    RE = EROSION RATE
C TIME0 = CALCULATED TIME (minutes) WHEN CONC. HAS DECREASED TO BE LESS
C         LESS THAN 1 MG/L DUE TO DEPOSITION OR WHEN DOWN-CORE CRITICAL
C         EROSION SHEAR STRESS HAS BECOME EQUAL TO THE BOTTOM SHEAR
C         STRESS SO THAT EROSION WILL STOP
C   QS0 = INITIAL MASS SEDIMENT TRANSPORT RATE
C   SED = TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME TRANSPORTED PER
C         UNIT BED WIDTH PER UNIT TIME (M**3/M/S)
C  CONC = FINAL CALCULATED SEDIMENT CONCENTRATION
C PHIM0 = INITIAL INTERNAL FRICTION ANGLE
C  PHIM = INTERNAL FRICTION ANGLE
C  TAO0 = INITIAL COMBINED SHEAR STRESS
C  TAOS = SOLID TRANSMITTED STRESS DUE TO ULVA
C   RES = ULVA RESUSPENSION PARAMETER
C
      G=9.81
      PI=2.*ASIN(1.)
C INITIALIZE PARAMETERS
      RD0=0.0
      RD=0.0
      RE0=0.0
      RE=0.0
      QS0=0.0
      TIME0=0.0
      SED=0.0
      SEDM=0.0
      CONC=0.0					!ccf

C ASSIGN TIME INCREMENT (IN SECONDS)
      DELTAT=300.

C CALCULATE INTERNAL FRICTION ANGLE PHI (DEGREES) FROM ZB0
C OF INITIAL CONDITION (BEFORE FURTHER EROSION)
C THIS ANGLE WILL BE USED FOR THE STRESS VERTICAL PROFILE OF THE BED
      IF (ZB0 .LE. 0.002) THEN
          PHIM0=0.
      ELSEIF (ZB0 .LE. 0.010 .AND. ZB0 .GT. 0.002) THEN
          PHIM0=30.
      ELSE
          PHIM0=10.
      ENDIF
      PHIM=PHIM0	

C CONVERT CONC0 FROM MG/L TO KG/M^3
      CONC0=CONC0/1000			!ggu bad -> FIXME

C  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 123

C*******************************
C  CURRENT ONLY (NO WAVES)
C*******************************

C COMPUTE THE INITIAL MASS TRANSPORT RATE (KG/M/S)
      QS0=CONC0*UA*D

C CALCULATE MAXIMUM CURRENT SHEAR STRESS
      TAO0=RHOW*USTCS**2

C CALCULATE SOLID TRANSMITTED STRESS DUE TO ULVA: TAOS
C USES P. COSETTE'S FORMULAE (MSC STUDENT, 2000, SOC) AND BAGNOLD'S
C APPROXIMATION FOR RESUSPENSION OF ULVA, WHERE TAUS=0
      TAOR=0.64*RHOW*WSULVA**2
      IF (TAO0 .LT. TAOR) THEN
         TAOS=AULVA*(159.4*TAO0-0.168)
         RES=0
      ENDIF
      IF (TAO0 .GE. TAOR) THEN
         TAOS=0
         RES=1
      ENDIF

C      TAOS=AULVA*(159.4*TAO0-0.168)

C CALCULATE NEW TOTAL SHEAR STRESS
      TAO0=TAO0+TAOS

C --------------------------------
C TAO0<TAOCD CASE
C --------------------------------
C FOR TAO0<TAOCD, DEPOSITION OCCURS AND NO EROSION
      IF (TAO0 .LT. TAOCD) THEN
         if (CONC0 .eq. 0.) THEN		!ccf
           GOTO 100
         end if

C ASSIGN VALUE TO THE RESUSPENSION PROBABILITY PRS
         PRS=0

C COMPUTE THE INITIAL DEPOSITION RATE RD0 (KG/M^2/S)
         RD0=CONC0*WS*(1-PRS)*(1-TAO0/TAOCD)

C INITIALIZE CONCENTRATION CHANGE
         CONCCHNG=0

C DETERMINE NUMERICAL INTEGRATION STEPS J IN 5 MINUTE INTERVALS
C         J=TIMEDR/5
C COMPUTE NUMERICAL INTEGRATION STEPS J AND TIMESTEP DELTAT IN 
C FUNCTION OF THE EROSION/DEPOSITION DURATION 		!ccf
          J=1
          IF (TIMEDR .GT. 5) J=TIMEDR/5 
          DELTAT=(TIMEDR/J)*60.

C INTEGRATING THE DECREASE OF CONCENTRATION DUE TO DEPOSITION 
         DO 10 IT=1,J
            IF ((CONC0-CONCCHNG) .LE. 0.001) THEN
C COMPUTE THE TIME (MINUTES) WHEN CONCENTRATION REACHED ZERO
               TIME0=IT*5
               CONCCHNG=CONC0			!ggu fix (CONC negative)
               RD = CONC0 * D / DELTAT		!ccf
               GOTO 20
            ENDIF
C CALCULATE STEP-WISE DEPOSITION RATE RD, DEPOSITED MASS DMASS,
C AND CONC CHANGE CONCCHNG
            RD=(CONC0-CONCCHNG)*WS*(1-PRS)*(1-TAO0/TAOCD)
            DMASS=RD*DELTAT
            CONCCHNG=CONCCHNG+DMASS/D
 10      CONTINUE

C COMPUTE THE FINAL CONCENTRATION
 20      CONC=CONC0-CONCCHNG
         GOTO 100
      ENDIF

C --------------------------------
C TAOCD<TAO0<TAOCE CASE
C --------------------------------
C FOR TAOCD<TAO0<TAOCE, THERE IS NO EROSION OR DEPOSITION
      IF (TAO0 .GE. TAOCD .AND. TAO0 .LT. TAOCE) THEN
C ASSIGN THE INITIAL CONCENTRATION AS THE CURRENT CONCENTRATION
         CONC=CONC0
         GOTO 100
      ENDIF

C --------------------------------
C TAO0>TAOCE CASE
C --------------------------------
C FOR TAO0>TAOCE, EROSION OCCURS AND NO DEPOSITION
      IF (TAO0 .GE. TAOCE) THEN

C DETERMINE NUMERICAL INTEGRATION STEPS J IN 5 MINUTE INTERVALS
C         J=TIMEDR/5
C COMPUTE NUMERICAL INTEGRATION STEPS J AND TIMESTEP DELTAT IN 
C FUNCTION OF THE EROSION/DEPOSITION DURATION 		!ccf
          J=1
          IF (TIMEDR .GT. 5) J=TIMEDR/5 
          DELTAT=(TIMEDR/J)*60.

C ASSIGN VALUE TO THE MINIMUM EROTION RATE E0
          E0=0.000051

C ASSIGN VALUE TO THE EROSION PROPORTIONALITY COEFFICIENT RKERO
C         RKERO=1.62

C COMPUTE THE INITIAL EROTION RATE
         RE0=E0*EXP(RKERO*(TAO0-TAOCE)**0.5)

C INITIALIZE THE TOTAL EROSION DEPTH "ZS" AND TOTAL EROSION MASS "EMASST" (KG/M^2)
         ZS=0
         EMASST=0

C BEGIN NUMERICAL INTEGRATION FOR TOTAL EROSION MASS "EMASST"
         DO 30 IT=1,J
C COMPUTE THE INCREASED CRITICAL EROSION SHEAR STRESS TAOCEZ AT ZS
            TAOCEZ=TAOCE+0.01*ZS*(RHOS-RHOW)*G*ATAN(PI*PHIM/180.)

C COMPUTE THE TIME WHEN TAOCEZ BECOMES EQUAL TO TAO0
C AND RECALCULATE THE NEW PHIM OF NEW BED LAYER EXPOSED
            IF (TAO0 .LT. TAOCEZ) THEN
               TIME0=5*IT
               GOTO 40
            ENDIF

C CALCULATE STEP-WISE EROSION RATE RE, ERODED MASS EMASS,
C AND EROSION DEPTHE EDEPTH
            RE=E0*EXP(RKERO*(TAO0-TAOCEZ)**0.5)
            EMASS=RE*DELTAT
            EDEPTH=EMASS/RHOS

C OBTAIN TOTAL ERODED MASS AND EROSION DEPTH
            EMASST=EMASST+EMASS
            ZS=ZS+EDEPTH

C "Z" IS THE NEW DEPTH OF THE BED LAYER EXPOSED
            Z=ZB0+ZS
            IF (Z .LE. 0.002) THEN
               PHIM=0.
            ELSEIF (Z .LE. 0.010 .AND. Z .GT. 0.002) THEN
               PHIM=30.
            ELSE
               PHIM=10.
            ENDIF
		
 30      CONTINUE
C COMPUTE THE FINAL CONCENTRATION
 40      CONC=CONC0+EMASST/D
      ENDIF

 100  CONTINUE
C OBTAIN FINAL MASS SEDIMENT TRANSPORT RATE (KG/M/S)
      SEDM=CONC*D*UA

      GOTO 321

 123  CONTINUE

C****************************
C COMBINED WAVE AND CURRENT
C****************************

C COMPUTE THE INITIAL MASS TRANSPORT RATE (KG/M/S)
      QS0=CONC0*U100*D

C CALCUTE COMBINED SHEAR STRESS
      TAOCWS=RHOW*USTCWS**2

C CALCULATE SOLID TRANSMITTED STRESS DUE TO ULVA: TAOS
C USES P. COSETTE'S FORMULAE (MSC STUDENT, 2000, SOC) AND BAGNOLD'S
C APPROXIMATION FOR RESUSPENSION OF ULVA, WHERE TAUS=0
      TAOR=0.64*RHOW*WSULVA**2
      IF (TAOCWS .LT. TAOR) THEN
         TAOS=AULVA*(159.4*TAOCWS-0.168)
         RES=0
      ENDIF
      IF (TAOCWS .GE. TAOR) THEN
         TAOS=0
         RES=1
      ENDIF

C	TAOS=AULVA*(159.4*TAOCWS-0.168)

C CALCULATE NEW TOTAL SHEAR STRESS
      TAOCWS=TAOCWS+TAOS
C
C --------------------------------
C TAOCWS<TAOCD CASE
C --------------------------------
C FOR TAOCWS<TAOCD, DEPOSITION OCCURS AND NO EROSION
      IF (TAOCWS .LT. TAOCD) THEN

         if (CONC0 .eq. 0.) THEN                !ccf
           GOTO 101
         end if

C ASSIGN VALUE TO THE RESUSPENSION PROBABILITY PRS
         PRS=0
C COMPUTE THE INITIAL DEPOSITION RATE RD0 (KG/M^2/S)
         RD0=CONC0*WS*(1-PRS)*(1-TAOCWS/TAOCD)
C INITIALIZE CONCENTRATION CHANGE
         CONCCHNG=0
C DETERMINE NUMERICAL INTEGRATION STEPS J IN 5 MINUTE INTERVALS
C         J=TIMEDR/5
C COMPUTE NUMERICAL INTEGRATION STEPS J AND TIMESTEP DELTAT IN 
C FUNCTION OF THE EROSION/DEPOSITION DURATION 		!ccf
          J=1
          IF (TIMEDR .GT. 5) J=TIMEDR/5 
          DELTAT=(TIMEDR/J)*60.

C INTEGRATING THE DECREASE OF CONCENTRATION DUE TO DEPOSITION
         DO 11 IT=1,J
            IF ((CONC0-CONCCHNG) .LE. 0.001) THEN
C COMPUTE THE TIME (MINUTES) WHEN CONCENTRATION DECREASED TO <1 MG/L
               TIME0=IT*5
               CONCCHNG=CONC0			!ggu fix (CONC negative)
               GOTO 21
            ENDIF
C CALCULATE STEP-WISE DEPOSITION RATE RD, DEPOSITED MASS DMASS,
C AND CONC CHANGE CONCCHNG
            RD=(CONC0-CONCCHNG)*WS*(1-PRS)*(1-TAOCWS/TAOCD)
            DMASS=RD*DELTAT
            CONCCHNG=CONCCHNG+DMASS/D
 11      CONTINUE
 21      CONC=CONC0-CONCCHNG
         GOTO 101
      ENDIF

C --------------------------------
C TAOCD<TAOCWS<TAOCE CASE
C --------------------------------
C FOR TAOCD<TAOCWS<TAOCE, THERE IS NO EROSION OR DEPOSITION
      IF (TAOCWS .GE. TAOCD .AND. TAOCWS .LT. TAOCE) THEN
C ASSIGN THE INITIAL CONCENTRATION AS THE CURRENT CONCENTRATION
         CONC=CONC0
         GOTO 101
      ENDIF

C --------------------------------
C TAOCWS>TAOCE CASE
C --------------------------------
C FOR TAOCWS>TAOCE, EROSION OCCURS AND NO DEPOSITION
      IF (TAOCWS .GE. TAOCE) THEN

C DETERMINE NUMERICAL INTEGRATION STEPS J IN 5 MINUTE INTERVALS
C         J=TIMEDR/5
C COMPUTE NUMERICAL INTEGRATION STEPS J AND TIMESTEP DELTAT IN 
C FUNCTION OF THE EROSION/DEPOSITION DURATION 		!ccf
          J=1
          IF (TIMEDR .GT. 5) J=TIMEDR/5 
          DELTAT=(TIMEDR/J)*60.

C ASSIGN THE DEFAULT VALUE TO THE MINIMUM EROSION RATE E0 (KG/M^2/S)
         E0=0.000051

C ASSIGN VALUE TO THE EROSION PROPORTIONALITY COEFFICIENT RKERO
C         RKERO=1.62
C COMPUTE THE INITIAL EROSION RATE RE0
C         RE0=E0*EXP(RKERO*(TAOCWS-TAOCE))
         RE0=E0*EXP(RKERO*(TAOCWS-TAOCE)**0.5)     !ggu fix (AG)

C INITIALIZE THE TOTAL EROSION DEPTH ZS AND TOTAL EROSION MASS EMASST (KG/M^2)
         ZS=0
         EMASST=0

C BEGIN NUMERICAL INTEGRATION FOR TOTAL EROSION MASS EMASST
         DO 31 IT=1,J
C COMPUTE THE INCREASED CRITICAL EROSION SHEAR STRESS TAOCEZ AT ZS
C           TAOCEZ=TAOCE+0.01*ZS*(RHOS-RHOW)*G*ATAN(PI*PHI/180.)
            TAOCEZ=TAOCE+0.01*ZS*(RHOS-RHOW)*G*ATAN(PI*PHIM/180.) !ggu fix (AG)
C COMPUTE THE TIME WHEN TAOCEZ BECOMES EQUAL TO TAOCWS (EROSION STOPS)
            IF (TAOCWS .LT. TAOCEZ) THEN
C              TIME0=1*IT
               TIME0=5*IT			!ggu fix (TIME0)
               GOTO 41
            ENDIF
C CALCULATE STEP-WISE EROSION RATE RE, ERODED MASS EMASS,
C AND EROSION DEPTHE EDEPTH
C            RE=E0*EXP(RKERO*(TAOCWS-TAOCEZ))
            RE=E0*EXP(RKERO*(TAOCWS-TAOCEZ)**0.5)    !ggu fix (AG)
            EMASS=RE*DELTAT
            EDEPTH=EMASS/RHOS
C OBTAIN TOTAL ERODED MASS AND EROSION DEPTH
            EMASST=EMASST+EMASS
            ZS=ZS+EDEPTH
C Z IS THE NEW DEPTH OF THE BED LAYER EXPOSED
            Z=ZB0+ZS
            IF (Z .LE. 0.002) THEN
                PHIM=0.
            ELSEIF (Z .LE. 0.010 .AND. Z .GT. 0.002) THEN
                PHIM=30.
            ELSE
                PHIM=10.
            ENDIF

 31      CONTINUE
C COMPUTE THE FINAL CONCENTRATION
 41      CONC=CONC0+EMASST/D
      ENDIF

 101  CONTINUE
C OBTAIN FINAL MASS SEDIMENT TRANSPORT RATE (KG/M/S)
      SEDM=CONC*D*U100

 321  CONTINUE
C CONVERT TO VOLUME TRANSPORT RATE (M^3/M/S)
      SED=SEDM/RHOS
C CONVERT CONCENTRATIONS BACK TO MG/LITRE
      CONC0=1000*CONC0
      CONC=1000*CONC

      RETURN
      END

C ******************************************************
